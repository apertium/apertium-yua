!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
Multichar_Symbols

%{V%}
%{l%}
%{h%}

%<v%>    ! Verbo

%<n%>    ! Sustantivo
%<adj%>  ! Adjetivo
%<aux%>  ! auxiliar
%<adp%>  ! Adposición
%<det%>  ! Determiner
%<sconj%>! subordinating conjunction
%<pl%>   ! Plural
%<s_pl%>   ! Sujeto Plural

%<prx%>  ! Proximal ʼthis _ʼ
%<med%>  ! Medial ʼthat _ʼ
%<dst%>  ! Distal ʼthat _ over thereʼ

%<incomp%> ! "ongoing incomplete"
%<past%> ! "Completed simple past"
%<perf%> ! "Completed Present Perfect"
%<subjun%> ! "Subjunctive"
%<pron%>  ! "pronoun"
%<aps%>   ! antipasivo
%<mv%>    ! middle voice
%<1sg_poss%>  ! "pronoun"
%<2sg_poss%>  ! "pronoun"
%<3sg_poss%>  ! "pronoun"
%<1pl_poss%>  ! "pronoun"
%<2pl_poss%>  ! "pronoun"
%<3pl_poss%>  ! "pronoun"

%<2p_pl%>
%<s_2sg%>
%<s_2pl%>
%<poss_nonownership%>
%<poss_comm%>
%<poss_internal%>

%<poss%>  ! "posesivo"
%<itg%>

%<clfanim%>
%<clfinan%>

%<hab%>
%<conj%>
%<pss%>
%<neg%>
%<adv%>
%<ipfv%> ! imperfectivo "Ongoing incomplete"
%<pfv%>  ! perfectivo "Simple Past"
%<pf%>   ! perfect "Completed Present"
%<subj%>  ! subjunctive
%<fut%>   ! futuro
%<s_sg%> ! sujeto singular
%<imp%>  ! imperativo
%<s_pl%> ! sujeto plural

%<s_sg1%>
%<s_sg2%>
%<s_sg3%>
%<s_pl1%>
%<s_pl2%>
%<s_pl3%>

%<o_sg1%>
%<o_sg2%>
%<o_sg3%>
%<o_pl1%>
%<o_pl2%>
%<o_pl3%>

%<pron_1sg%>
%<pron_2sg%>
%<pron_3sg%>
%<pron_1pl%>
%<pron_2pl%>
%<pron_3pl%>
%<intj%>

%<female_agentive%>
%<male_agentive%>

%<tv%>            !
%<iv%>            ! 

%<attr%>          ! Attributive
%<der_l%>         ! Adjective derivation
%<term%>          ! Terminative morpheme
%<top%>          ! Topicalized
%<loc%>           ! Locative

%<pos%> ! Positional

%<num%>

%<hab%>
%<dur%>
%<cont%>
%<cess%>
%<incep%>
%<des%>
%<oblig%>  ! obligative mood

%<propn%> ! Proper Noun

%{o%}           ! Either ʼoʼ or nothing
%{e%}           ! Either ʼeʼ or nothing
%{V%}           ! Copy stem vowel
%{wy%}          ! start to vowel-initial verb stems either y- or w- depending on person of subject.

ʼ               ! Glottal, U+02BC
%>              ! Morpheme boundary 
0

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root
VerbandNounInit ;
SetA ;
PronombresIndependientes ;
Negative ;
AspectMoodMarker ;
Adjectives ;
Adverb ;
NominalClassifiers ;
Numbers ;
Interrogativos ;
Adposition ;
Interjections ;
Determiner ;
Conjunction ;

LEXICON VerbandNounInit

! TODO: flag diacritics so that these only happen in the imperfective,
! and w only 1st/2nd persons, y only 3rd persons
0:w VerborPossessiveNoun ;
0:y VerborPossessiveNoun ;
VerborPossessiveNoun ;

LEXICON VerborPossessiveNoun
CvcTrans ;
IntransReg ;
IrregularTransVerb ;
IrregularIntransVerb ;
Positionals ;
! Agentives ;
Noun ;
ProperNoun ;

LEXICON AspectMoodMarker
ImperfectiveAspectMoodMarker ;
SubjunctiveAspectMoodMarker ;
PerfectiveAspectMoodMarker ;

!LEXICON Agentives
! Noun ;

LEXICON ImperfectiveAspectMoodMarker
k%<aux%>%<hab%>:k # ;
k%<aux%>%<hab%>%<neg%>:t # ;


táan%<aux%>%<dur%>:táan # ;
taan%<aux%>%<dur%>:taan # ;
lailiʼ%<aux%>%<cont%>:lailiʼ # ;

tsʼoʼok%<aux%>%<cess%>:tsʼoʼok # ;
tsʼoʼok%<aux%>%<cess%>:tsʼoʼokih # ;

taant%<aux%>%<cess%>:taant # ; ! how to distinguish this proximate terminative?
hoʼop%<aux%>%<incep%>:hoʼop # ;
kaah%<aux%>%<incep%>:kaah # ;
chuun%<aux%>%<incep%>:chuun # ;
lek%<aux%>%<incep%>:lek # ;

taak%<aux%>%<des%>:taak # ;
tàak%<aux%>%<des%>:tàak # ;
táak%<aux%>%<des%>:táak # ;

kʼabéet%<aux%>%<oblig%>:kʼabéet # ;
kʼabéet%<aux%>%<oblig%>:kʼabeet # ;

kʼaʼnaʼan%<aux%>%<oblig%>:kʼaʼanaʼan # ;
yàan%<aux%>%<oblig%>:yàan # ;
paʼatik%<aux%>%<fut%>:paʼatik # ;

heʼel%<aux%>%<fut%>:heʼel # ;  ! takes ...-e' at phrase boundary
heʼel%<aux%>%<fut%>:heʼ # ;  ! takes ...-e' at phrase boundary

koʼox%<aux%>%<imp%>:koʼox #;
koʼox%<aux%>%<imp%>%<s_pl%>:koʼox%>eʼex #;

kaʼach%<aux%>:kaʼach #;

LEXICON SubjunctiveAspectMoodMarker
kaʼah%<aux%>:kaʼah # ; ! might have/would have/could have
òolak%<aux%>:òolak # ; ! almost
pʼelak%<aux%>:pʼelak # ; ! as soon as

bíin%<aux%>:bíin # ;
úuch%<aux%>:úuch # ;
sáan%<aux%>:sáan # ;


LEXICON PerfectiveAspectMoodMarker
t%<pfv%>%<tv%>:t # ;
t%<aux%>%<pfv%>%<tv%>%+in%<s_sg1%>%<pron%>:tin # ;
t%<aux%>%<pfv%>%<tv%>%+a%<s_sg2%>%<pron%>:ta # ;
t%<aux%>%<pfv%>%<tv%>%+u%<s_sg3%>%<pron%>:tu # ; 
h%<aux%>%<pfv%>%<iv%>:h # ;

LEXICON Negative
maʼa%<neg%>:maʼa # ;
maʼ%<neg%>:maʼ # ;
maʼat%<hab%>%<neg%>:maʼat # ;
ma%<neg%>:ma # ;

LEXICON Determiner
le%<det%>:le # ;
heʼel%<det%>:heʼel Terminatives ;
heʼel%<det%>:heʼ #;
teʼel%<det%>:teʼel Terminatives;
teʼel%<det%>:teʼ #;

LEXICON SetA
in%<s_sg1%>%<pron%>:in # ;
k%<hab%>%+in%<s_sg1%>%<pron%>:k%>in # ;
k%<hab%>%<neg%>+in%<s_sg1%>%<pron%>:k%>in # ;
táan%<dur%>%+in%<s_sg1%>%<pron%>:tin # ;
tiʼi%<adp%>%+in%<s_sg1%>%<pron%>:t%>in # ;
t%<pfv%>%+in%<s_sg1%>%<pron%>:t%>in # ;
tiʼi%<adp%>%+in%<1sg_poss%>%<pron%>:t%>in # ;
in%<1sg_poss%>%<pron%>:in # ;

! todo: add t+SETA contractions with possessive analysis
a%<s_sg2%>%<pron%>:a # ;
k%<hab%>%+a%<s_sg2%>%<pron%>:k%>a # ;
táan%<dur%>%+%<s_sg2%>%<pron%>:tan # ;
t%<pfv%>%+a%<s_sg2%>%<pron%>:t%>a # ;
tiʼi%<adp%>%+a%<s_sg2%>%<pron%>:t%>a # ;

u%<s_sg3%>%<pron%>:u # ;
k%<hab%>%+u%<s_sg3%>%<pron%>:k%>u # ;

k%<s_pl1%>%<pron%>:k # ;

a%<s_pl2%>%<pron%>:a # ;
k%<hab%>%+a%<s_pl2%>%<pron%>:k%>a # ;
táan%<dur%>%+%<s_pl2%>%<pron%>:tan # ;
t%<pfv%>%+a%<s_pl2%>%<pron%>:t%>a # ;
tiʼi%<adp%>%+a%<s_pl2%>%<pron%>:t%>a # ;

u%<s_pl3%>%<pron%>:u # ;
tiʼi%<adp%>%+u%<s_sg3%>%<pron%>:t%>u # ;
tiʼi%<adp%>%+u%<s_pl3%>%<pron%>:t%>u # ;


a%<2sg_poss%>%<pron%>:a # ;
u%<3sg_poss%>%<pron%>:u # ;
k%<1pl_poss%>%<pron%>:k # ;
a%<2pl_poss%>%<pron%>:a # ;
u%<3pl_poss%>%<pron%>:u # ;

t%<aux%>%<hab%>%<neg%>+in%<s_sg1%>%<pron%>:tin # ;
t%<aux%>%<hab%>%<neg%>+a%<s_sg2%>%<pron%>:ta # ;
t%<aux%>%<hab%>%<neg%>+u%<s_sg3%>%<pron%>:tu # ;

LEXICON PronombresIndependientes
ten%<pron_1sg%>:téen Topicalizer ;
ten%<pron_1sg%>:tèen Topicalizer ;
ten%<pron_1sg%>:ten Topicalizer ;

tech%<pron_2sg%>:téech Topicalizer ;
tech%<pron_2sg%>:tèech Topicalizer ;
tech%<pron_2sg%>:tech Topicalizer ;

letiʼ%<pron_3sg%>:letiʼ Topicalizer ;

toʼon%<pron_1pl%>:toʼon Topicalizer ;
teʼex%<pron_2pl%>:teʼex Topicalizer ;
letiʼ%<pron_3pl%>:letiʼ%>oʼob Topicalizer ;

minaʼan%<neg%>%<pron%>:minaʼan Topicalizer ; ! nada, no hay nada

LEXICON Topicalizer
%<top%>:%>eʼ # ;

Terminatives ;

LEXICON TransitiveSetB
%<o_sg1%>:%>en PluralSubjSuffix ;
%<o_pl1%>:%>oʼon PluralSubjSuffix ;
%<o_sg2%>:%>ech PluralSubjSuffix ;
%<o_pl2%>:%>eʼex PluralSubjSuffix ;
%<o_sg1%>:%>en PluralSubjSuffix ;
%<o_sg3%>:0 PluralSubjSuffix ;
%<o_sg3%>:%>ih PluralSubjSuffix ;
%<o_pl3%>:%>oʼob PluralSubjSuffix ;
Reflexives ;

LEXICON Reflexives
%<refl_1sg%>:%>in%>ba%{h%} Terminatives ;
%<refl_2sg%>:%>a%>ba%{h%} Terminatives ;
%<refl_3sg%>:%>u%>ba%{h%} Terminatives ;
%<refl_1pl%>:%>k%>ba%{h%} Terminatives ;
%<refl_2pl%>:%>a%>ba%{h%}%>eʼex Terminatives ;
%<refl_3pl%>:%>u%>ba%{h%}%>oʼob Terminatives ;

LEXICON IntransitiveSetB
%<s_sg1%>:%>en Terminatives ;
%<s_pl1%>:%>oʼon Terminatives ;
%<s_sg2%>:%>ech Terminatives ;
%<s_pl2%>:%>eʼex Terminatives ;
%<s_sg1%>:%>en Terminatives ;
%<s_sg3%>:0 Terminatives ;
%<s_pl3%>:%>oʼob Terminatives ;

LEXICON PluralSubjSuffix
%<s_pl3%>:%>oʼob Terminatives ;
%<s_pl2%>:%>eʼex Terminatives ;
Terminatives ;

LEXICON DeicticTerminatives
%<prx%>:%>aʼ # ;
%<dst%>:%>oʼ # ;
# ;

LEXICON Terminatives
%<neg%>:%>iʼ # ;
%<loc%>:%>iʼ # ;
%<top%>:%>eʼ # ;
# ;

! these are endings to the aspect markers laili and taant
%<cont%>:%>eʼ # ; ! Laili' 
%<cess%>:%>eʼ # ; ! taant 
%<fut%>:%>eʼ # ; ! he'el 

DeicticTerminatives ;
# ;

LEXICON RegIntransVozActivaSinL
%<ipfv%>:0 IntransitiveSetB;
%<pfv%>:0 IntransitiveSetB;
%<pf%>:%>ahaʼan IntransitiveSetB;
%<subj%>:%>%{V%}k IntransitiveSetB;
%<imp%>%<s_2sg%>:%>en Terminatives;
%<imp%>%<s_2pl%>:%>en%>eʼex Terminatives;

LEXICON RegIntransVozActivaConL
%<ipfv%>:%>%{V%}l PluralSubjSuffix;
%<pfv%>:0 IntransitiveSetB;
%<pf%>:%>ahaʼan IntransitiveSetB;
%<subj%>:%>%{V%}k IntransitiveSetB;
%<imp%>%<s_2sg%>:%>en Terminatives;
%<imp%>%<s_2pl%>:%>eneʼex Terminatives;

LEXICON RegIntransDvT
%<ipfv%>:%>ik TransitiveSetB;
%<pfv%>:%>ah TransitiveSetB;
%<pf%>:%>mah TransitiveSetB;
%<subj%>:0 TransitiveSetB;
%<imp%>%<s_2sg%>:%>eh Terminatives;
%<imp%>%<s_2sg%>:0 Terminatives;
%<imp%>%<s_2pl%>:0 PluralSubjSuffix;

LEXICON RegIntransDvTVozPasiva
%<ipfv%>:%>%{V%}l PluralSubjSuffix;
%<pfv%>:0 IntransitiveSetB;
%<pf%>:%>aʼan IntransitiveSetB;
%<subj%>:%>ak IntransitiveSetB;

! TODO: check on this....whatʼs the plural ending?
%<imp%>:%>aʼak Terminatives;

LEXICON RegIntransDvTVozAntipasiva

%<ipfv%>:%>ah IntransitiveSetB;
%<pfv%>:%>ah%>nah IntransitiveSetB;
%<pf%>:%>ah%>nahaʼan IntransitiveSetB;
%<subj%>:%>ah%>nak IntransitiveSetB;

! TODO: check on this....whatʼs the plural ending?
%<imp%>:%>ahnak Terminatives;

LEXICON TransVozActiva
%<ipfv%>:%>ik TransitiveSetB;
%<pfv%>:%>ah TransitiveSetB;
%<pf%>:%>mah TransitiveSetB;
%<subj%>:0 TransitiveSetB;
%<subj%>:%>eh TransitiveSetB;
%<imp%>:0 TransitiveSetB;
%<imp%>:%>eh TransitiveSetB;


LEXICON TransVozAntipasiva
%<ipfv%>:0 PluralSubjSuffix;

! TODO: for all of the following entries, the geminate "n%>n -> n"
%<pfv%>:%>nah IntransitiveSetB;
%<pf%>:%>nahaʼan IntransitiveSetB;
%<subj%>%<s_sg%>:%>nak IntransitiveSetB;
%<imp%>:%>nen IntransitiveSetB;

LEXICON TransVozPasiva
%<ipfv%>:%>%{V%}l PluralSubjSuffix;
%<pfv%>:%>aʼab IntransitiveSetB;
%<pf%>:%>aʼan IntransitiveSetB;
%<subj%>:%>%{V%}k IntransitiveSetB;
! %<imp%>:%{V%}k IntransitiveSetB;  ! this is kind of weird, eg "be loved!" TODO verify that it is a valid form

LEXICON IrregularTransVozPasiva
%<ipfv%>:%>aʼal PluralSubjSuffix;
%<pfv%>:%>aʼab IntransitiveSetB;
%<pf%>:%>aʼan IntransitiveSetB;
%<subj%>:%>aʼak IntransitiveSetB;
! %<imp%>:aʼak IntransitiveSetB;

LEXICON TransVozMedia
%<ipfv%>:%>%{V%}l PluralSubjSuffix;
%<pfv%>:0 IntransitiveSetB;
%<pf%>:%>aʼan IntransitiveSetB;
%<subj%>:%>%{V%}k IntransitiveSetB;
%<imp%>:0 IntransitiveSetB;


LEXICON PossessiveNominalSuffixes
%<poss_comm%>:%>il IntransitiveSetB ;
%<poss_nonownership%>:%>al IntransitiveSetB ;
%<poss_internal%>:%>el IntransitiveSetB ;
PluralSuffix ;
IntransitiveSetB ;

LEXICON PluralSuffix
%<pl%>:%>oʼob Terminatives ;
%<2p_pl%>:%>eʼex Terminatives ;
# ;

LEXICON NounEndings
Terminatives ;
PluralSuffix ;
IntransitiveSetB ;
PossessiveNominalSuffixes ;
# ;

LEXICON NominalClassifiers
túul%<clfanim%>:tuul PluralSubjSuffix ;
túul%<clfanim%>:túul PluralSubjSuffix ;

pʼéel%<clfinan%>:pʼéel PluralSubjSuffix ;
hun%<clfinan%>%+pʼéel%<num%>:humpʼéel # ;
hun%<clfinan%>%+pʼéel%<num%>:hunpʼéel # ;
hun%<clfanim%>%+túul%<num%>:huntúul # ;

LEXICON Numbers
hun%<num%>:hun # ;
kaʼa%<num%>:kaʼa # ;
óox%<num%>:óox # ;
kam%<num%>:kam # ;
hó%<num%>:hó # ;

LEXICON Adposition
tak%<adp%>:tak # ; ! hasta
tiʼi%<adp%>:tiʼ PluralSuffix ; ! to _ (introduces indirect obj or location)
tiʼi%<adp%>:tʼ PluralSuffix ; ! to _ (introduces indirect obj or location)
teʼe%<adp%>:teʼ PluralSuffix ; ! to _ (introduces indirect obj)
tumèen%<adp%>:tumèen # ; ! por

LEXICON TerminacionesInterrogativas
0:%>ih # ;
Terminatives ;
# ;

LEXICON Interrogativos
baʼax%<pron%>%<itg%>:baʼax TerminacionesInterrogativas ; !qué
baʼax%<pron%>%<itg%>:baʼan TerminacionesInterrogativas ; !qué
baʼax%<pron%>%<itg%>:baax TerminacionesInterrogativas ; !qué
baʼax%<pron%>%<itg%>:baan TerminacionesInterrogativas ; !qué

bix%<adv%>%<itg%>:bix TerminacionesInterrogativas ; ! cómo
tuʼux%<adv%>%<itg%>:tuʼux TerminacionesInterrogativas ; ! dónde
máax%<pron%>%<itg%>:máax TerminacionesInterrogativas ; ! quién
máakalmáak%<pron%>%<itg%>:máakalmáak TerminacionesInterrogativas ; ! quién
ba’axten%<adv%>%<itg%>:ba’axten TerminacionesInterrogativas ; ! por qué
ba’axten%<adv%>%<itg%>:ba’anten TerminacionesInterrogativas ; ! por qué



LEXICON AdverbialEndings
Terminatives ;
# ;

LEXICON Adverb
xan%<adv%>:xan AdverbialEndings ; ! also/too
sáansamal%<adv%>:sáansamal AdverbialEndings; ! everyday
sáamal%<adv%>:sáamal AdverbialEndings; ! tomorrow
bey%<adv%>:bey DeicticTerminatives ; ! like so
chen%<adv%>:chen # ; ! "just"
chen%<adv%>:chéen # ; ! "just"
chan%<adv%>:chan # ; ! "poquito"

mix%<adv%>:mix # ; ! "ni, negativo"

séeb%<adv%>:séeb # ; ! "quickly"

behláʼ%<adv%>:behláʼ AdverbialEndings ; ! hoy
beʼelaʼ%<adv%>:beʼelaʼ AdverbialEndings ;

haʼatsʼkab%<adv%>:haʼatsʼkab AdverbialEndings ; ! "(in the) early morning"
way%<adv%>:way AdverbialEndings ; ! "aquí"
hach%<adv%>:hach AdverbialEndings ; ! "muy..."
chanbéelil%<adv%>:chanbéelil AdverbialEndings ; ! despacio
kaʼakaʼat%<adv%>:kaʼakaʼat AdverbialEndings ; ! después


beyòoraʼa%<adv%>:beyòoraʼa AdverbialEndings; ! now
beyòoraʼa%<adv%>:beyòoraʼ AdverbialEndings; ! now
beyòoraʼa%<adv%>:beyòorah AdverbialEndings; ! now
behòoraʼa%<adv%>:behòoraʼa AdverbialEndings; ! now
behòoraʼa%<adv%>:behòoraʼ AdverbialEndings; ! now
behòoraʼa%<adv%>:behòorah AdverbialEndings; ! now

túun%<adv%>:túun AdverbialEndings; ! entonces
wa%<adv%>:wa # ;

LEXICON Conjunction
kux%<conj%>:kux # ; ! y...
yeetel%<conj%>:yeetel Terminatives ;
yeetel%<sconj%>:yeetel Terminatives ;
ka%<sconj%>:ka # ;
wa%<sconj%>:wa # ;
utiaʼal%<sconj%>:utiaʼal # ;

LEXICON Noun
tsíimin%<n%>:tsíimin NounEndings ; ! "caballo"
káambal%<n%>:káambal NounEndings ; ! "estudiante"
kaʼansah%<n%>:kaʼansah NounEndings ; ! "maestro"
kòolnáal%<n%>:kòolnáal NounEndings ; ! "agricultor(a)"
wíinik%<n%>:wíinik NounEndings ; ! "humano"
máak%<n%>:máak NounEndings ; ! "persona"
míis%<n%>:míis NounEndings ; ! "gato"
pèek%<n%>:pèek NounEndings ; ! "perro"
huʼunalte%<n%>:huʼunalte NounEndings ; ! "libro"
ùulum%<n%>:ùulum NounEndings ; ! "huajolote"
bàatsil%<n%>:bàatsil NounEndings ; ! "amigo"
kʼàaba%<n%>:kʼàaba NounEndings ; ! "nombre"
ich%<n%>:ich NounEndings ; ! "ojo"
òon%<n%>:òon NounEndings ; ! "aguacate"

óol%<n%>:óol NounEndings ; ! "corazón"
ool%<n%>:ool NounEndings ; ! "corazón"

nah%<n%>:nah NounEndings ; ! "casa"
xòok%<n%>:xòok NounEndings ; ! "lesson, studying"
chùumuk%<n%>:chùumuk NounEndings ; ! "middle/center"
kìisbùutsʼ%<n%>:kìisbùutsʼ NounEndings ; ! "car"
kʼíiwik%<n%>:kʼíiwik NounEndings ; ! "town center"
kʼìin%<n%>:kʼìin NounEndings ; 
éet%<n%>:éet NounEndings ; ! compañero...se antepone a otra palabra (ej. in wèet meyah "mi compañero de trabajo")
láak%<n%>:láak NounEndings ; ! todo
láakʼ%<n%>:láakʼ NounEndings ; ! hermana/o
koʼolel%<n%>:koʼolel NounEndings ; ! mujer
kíim%<n%>:kíim NounEndings ; ! muerto
baʼal%<n%>:baʼal NounEndings ; ! cosa
àamigah%<n%>:àamigah NounEndings ; ! amiga
àamigoh%<n%>:àamigoh NounEndings ; ! amigo
chúumuk%<n%>:chúumuk NounEndings ; ! centro
chúumuk%<n%>:chumuk NounEndings ; ! centro (variación ortográfica)
kʼàan%<n%>:kʼàan NounEndings ; ! hamaca
chukwa%<n%>:chukwa NounEndings ; ! chocolate
óotsʼil%<n%>:óotsʼil NounEndings ; ! persona pobre
óotsʼil%<n%>:òotsʼil NounEndings ; ! persona pobre
lu’um%<n%>:lu’um NounEndings ; ! tierra, territorio
taʼab%<n%>:taʼab NounEndings ; ! sal
tʼaan%<n%>:tʼaan NounEndings ; ! palabra
pʼíit%<n%>:pʼíit NounEndings ; ! un poco
xkuruch%<n%>:xkuruch NounEndings ; ! cucaracha
otoch%<n%>:otoch NounEndings ; ! hogar
áakʼab%<n%>:áakʼab NounEndings ; ! noche
buʼul%<n%>:buʼul NounEndings ; ! frijoles

ixiʼim%<n%>:ixiʼim NounEndings ; ! maiz
ixiʼim%<n%>:ixim NounEndings ; ! maiz

holnah%<n%>:holnah NounEndings ; ! ventana/puerta

íicham%<n%>:íicham NounEndings ; ! esposo
chʼoʼ%<n%>:chʼoʼ NounEndings ; ! ratón
beh%<n%>:beh NounEndings ; ! road, way
beh%<n%>:béeh NounEndings ; ! road, way
bèel%<n%>:bèel NounEndings ; ! road, occupation
bèel%<n%>:bel NounEndings ; ! road, variation
awat%<n%>:awat NounEndings ; ! grito
tʼèel%<n%>:tʼèel NounEndings ; ! gallo

! TODO: maybe just haah?
hàahil%<n%>:hàahil NounEndings ; ! verdad

SpanishNouns ;

LEXICON SpanishNouns
cuchara%<n%>:cuchara NounEndings ; ! cuchara
vecina%<n%>:vecina NounEndings ; ! cuchara
òorah%<n%>:òorah NounEndings ; ! hora
polvio%<n%>:polvio NounEndings ; ! maquillaje
ìingles%<n%>:ìingles NounEndings ; ! inglés


LEXICON ProperNoun
cancún%<propn%>:cancún NounEndings; ! Cancún
cancun%<propn%>:cancun NounEndings; ! Cancún
chichén%<propn%>:chichén NounEndings ; ! Chichén itza
isma%<propn%>:isma NounEndings; ! Isma (name)
ho%<propn%>:ho NounEndings; ! ho
hó%<propn%>:hó NounEndings; ! ho
chicago%<propn%>:chicago NounEndings; ! chicago
pisté%<propn%>:pisté NounEndings ; ! pisté
pisté%<propn%>:pʼisteʼ NounEndings ; ! piste
pisté%<propn%>:pʼisté NounEndings ; ! piste

dios%<propn%>:dios NounEndings; ! dios 
dios%<propn%>:dyòos NounEndings; ! dios
pil%<propn%>:pil NounEndings; ! pil (name)
tony%<propn%>:Tony NounEndings; ! tony (name)
doña%<propn%>:doña NounEndings; ! doña
jacinta%<propn%>:jacinta NounEndings; ! jacinta (name)
goyo%<propn%>:goyo NounEndings; ! jacinta (name)
màaya%<propn%>:màaya NounEndings ; ! maya
felipe%<propn%>:felipe NounEndings ; ! sarah

sarah%<propn%>:sarah NounEndings ; ! sarah
laura%<propn%>:laura NounEndings ; ! laura

xpil%<propn%>:xpil NounEndings ; ! xpil. TODO support x- as prefix

zaci%<propn%>:zaci NounEndings ; ! zaci
tomas%<propn%>:tomas NounEndings ; ! tomas
carmen%<propn%>:carmen NounEndings ; ! carmen
julia%<propn%>:julia NounEndings ; ! julia
fidelia%<propn%>:fidelia NounEndings ; ! fidelia

chʼochʼ%<n%>:chʼochʼ NounEndings ; ! cigarra
chʼochʼlin%<n%>:chʼochʼlin NounEndings ; ! cigarra

síinik%<n%>:síinik NounEndings ; ! hormiga

LEXICON IntransReg
! go
bin%<v%>%<iv%>:bin RegIntransVozActivaSinL ;
bin%<v%>%<tv%>:bin%>s RegIntransDvT ;
bin%<v%>%<tv%>:bi%>s RegIntransDvT ;
bin%<v%>%<iv%>%<pss%>:bin%>s RegIntransDvTVozPasiva ;
bin%<v%>%<iv%>%<aps%>:bin%>s RegIntransDvTVozAntipasiva ;

! barrer
míis%<v%>%<iv%>:míis RegIntransVozActivaSinL ;
míis%<v%>%<tv%>:míis%>t RegIntransDvT ;
míis%<v%>%<iv%>%<pss%>:míis%>t RegIntransDvTVozPasiva ;
míis%<v%>%<iv%>%<aps%>:míis DeadjectivalVerbalEndings ;

! wait
páakʼ%<v%>%<iv%>:páakʼ RegIntransVozActivaSinL ;
páakʼ%<v%>%<tv%>:páakʼ%>t RegIntransDvT ;
páakʼ%<v%>%<iv%>%<pss%>:páakʼ%>t RegIntransDvTVozPasiva ;
páakʼ%<v%>%<iv%>%<aps%>:páakʼ%>t RegIntransDvTVozAntipasiva ;

páakʼ%<v%>%<tv%>:paʼa%>t RegIntransDvT ;
páakʼ%<v%>%<tv%>:paʼa%>t RegIntransDvTVozPasiva ;
páakʼ%<v%>%<tv%>:paʼa%>t RegIntransDvTVozAntipasiva ;

! descansar
heʼel%<v%>%<iv%>:heʼel RegIntransVozActivaConL ;
heʼel%<v%>%<tv%>:heʼe%{l%}%>s RegIntransDvT ;
heʼel%<v%>%<iv%>%<pss%>:heʼe%{l%}%>s RegIntransDvTVozPasiva ;
heʼel%<v%>%<iv%>%<aps%>:heʼe%{l%}%>s RegIntransDvTVozAntipasiva ;

! salir
hóok%<v%>%<iv%>:hóok RegIntransVozActivaConL ;
hóok%<v%>%<tv%>:hóok%>s RegIntransDvT ;
hóok%<v%>%<iv%>%<pss%>:hóok%>s IrregularTransVozPasiva ;
hóok%<v%>%<iv%>%<aps%>:hóok%>s RegIntransDvTVozAntipasiva ;

! comenzar
hoʼop%<v%>%<iv%>:hoʼop RegIntransVozActivaConL ;
hoʼop%<v%>%<tv%>:hoʼop%>s RegIntransDvT ;
hoʼop%<v%>%<iv%>%<aps%>:hoʼop%>s RegIntransDvTVozAntipasiva ;
hoʼop%<v%>%<tv%>:hoʼop%>be%>s RegIntransDvT ;
hoʼop%<v%>%<iv%>%<aps%>:hoʼop%>be%>s RegIntransDvTVozAntipasiva ;


! descansar
báax%<v%>%<iv%>:báax RegIntransVozActivaConL ;
báax%<v%>%<tv%>:báax%>t RegIntransDvT ;
báax%<v%>%<iv%>%<pss%>:báax%>t RegIntransDvTVozPasiva ;


! arrive
tàal%<v%>%<iv%>:tàal RegIntransVozActivaConL ;
tàal%<v%>%<tv%>:tàa%{l%}%>s RegIntransDvT ;
tàal%<v%>%<iv%>%<pss%>:tàa%{l%}%>s RegIntransDvTVozPasiva ;
tàal%<v%>%<iv%>%<aps%>:tàa%{l%}%>s RegIntransDvTVozAntipasiva ;

! go about (pasear)
máan%<v%>%<iv%>:máan RegIntransVozActivaConL ;
máan%<v%>%<tv%>:máan%>s RegIntransDvT ;
máan%<v%>%<iv%>%<pss%>:máan%>s RegIntransDvTVozPasiva ;
máan%<v%>%<iv%>%<aps%>:máan%>s RegIntransDvTVozAntipasiva ;

! work
meyah%<v%>%<iv%>:meyah RegIntransVozActivaConL ;
meyah%<v%>%<tv%>:meyah%>t RegIntransDvT ;
meyah%<v%>%<iv%>%<pss%>:meyah%>t RegIntransDvTVozPasiva ;
meyah%<v%>%<iv%>%<aps%>:meyah%>t RegIntransDvTVozAntipasiva ;

! hacer
mèen%<v%>%<iv%>:mèen RegIntransVozActivaConL ;
mèen%<v%>%<tv%>:mèen%>t RegIntransDvT ;
mèen%<v%>%<iv%>%<pss%>:mèen%>t RegIntransDvTVozPasiva ;
mèen%<v%>%<iv%>%<aps%>:mèen%>t RegIntransDvTVozAntipasiva ;

! sleep
wen%<v%>%<iv%>:wen RegIntransVozActivaConL ;
wen%<v%>%<tv%>:wen%>s RegIntransDvT ;
wen%<v%>%<iv%>%<pss%>:wen%>s RegIntransDvTVozPasiva ;
wen%<v%>%<iv%>%<aps%>:wen%>s RegIntransDvTVozAntipasiva ;

! enter
ok%<v%>%<iv%>:ok RegIntransVozActivaConL ;
ok%<v%>%<tv%>:ok%>s RegIntransDvT ;
ok%<v%>%<iv%>%<pss%>:ok%>s RegIntransDvTVozPasiva ;
ok%<v%>%<iv%>%<aps%>:ok%>s RegIntransDvTVozAntipasiva ;

! enter/rob
òok%<v%>%<iv%>:òok RegIntransVozActivaConL ;
òok%<v%>%<tv%>:òok%>s RegIntransDvT ;
òok%<v%>%<iv%>%<pss%>:òok%>s RegIntransDvTVozPasiva ;
òok%<v%>%<iv%>%<aps%>:òok%>s RegIntransDvTVozAntipasiva ;

! wake up
ah%<v%>%<iv%>:%{wy%}ah RegIntransVozActivaConL ;
ah%<v%>%<tv%>:%{wy%}ah%>s RegIntransDvT ;
ah%<v%>%<iv%>%<pss%>:%{wy%}ah%>s RegIntransDvTVozPasiva ;
ah%<v%>%<iv%>%<aps%>:%{wy%}ah%>s RegIntransDvTVozAntipasiva ;

! finish, to be done
tsʼoʼok%<v%>%<iv%>:tsʼoʼok RegIntransVozActivaConL ;
tsʼoʼok%<v%>%<tv%>:tsʼoʼok%>s RegIntransDvT ;
tsʼoʼok%<v%>%<iv%>%<pss%>:tsʼoʼok%>s RegIntransDvTVozPasiva ;
tsʼoʼok%<v%>%<iv%>%<aps%>:tsʼoʼok%>s RegIntransDvTVozAntipasiva ;

! reply
núuk%<v%>%<iv%>:núuk RegIntransVozActivaConL ;
núuk%<v%>%<tv%>:núuk RegIntransDvT ;
núuk%<v%>%<iv%>%<pss%>:núuk%>s RegIntransDvTVozPasiva ;
núuk%<v%>%<iv%>%<aps%>:núuk%>s RegIntransDvTVozAntipasiva ;

! pensar
tùuk%<v%>%<iv%>:tùuk RegIntransVozActivaConL ;
tùuk%<v%>%<tv%>:tùuk RegIntransDvT ;
tùuk%<v%>%<iv%>%<pss%>:tùuk%>s RegIntransDvTVozPasiva ;
tùuk%<v%>%<iv%>%<aps%>:tùuk%>s RegIntransDvTVozAntipasiva ;

! escribir
tsʼíib%<v%>%<iv%>:tsʼíib RegIntransVozActivaConL ;
tsʼíib%<v%>%<tv%>:tsʼíib%>t RegIntransDvT ;
tsʼíib%<v%>%<iv%>%<pss%>:tsʼíib%>t RegIntransDvTVozPasiva ;
tsʼíib%<v%>%<iv%>%<aps%>:tsʼíib%>t RegIntransDvTVozAntipasiva ;

! enter
ok%<v%>%<iv%>:ok RegIntransVozActivaConL ;
ok%<v%>%<tv%>:ok%>s RegIntransDvT ;
ok%<v%>%<iv%>%<pss%>:ok%>s RegIntransDvTVozPasiva ;
ok%<v%>%<iv%>%<aps%>:ok%>s RegIntransDvTVozAntipasiva ;
ok%<v%>%<iv%>%<aps%>:óok RegIntransDvTVozAntipasiva ;

! exit
hóokʼ%<v%>%<iv%>:hóokʼ RegIntransVozActivaConL ;
hóokʼ%<v%>%<tv%>:hóokʼ%>s RegIntransDvT ;
hóokʼ%<v%>%<iv%>%<pss%>:hóokʼ%>s RegIntransDvTVozPasiva ;
hóokʼ%<v%>%<iv%>%<aps%>:hóokʼ%>s RegIntransDvTVozAntipasiva ;

! chat
tsikbal%<v%>%<iv%>:tsikbal TransVozAntipasiva ;
tsikbal%<v%>%<tv%>:tsikba%{l%}%>t RegIntransDvT ;
tsikbal%<v%>%<iv%>%<pss%>:tsikba%{l%}%>t TransVozPasiva ;


! swim
báab%<v%>%<iv%>:báab RegIntransVozActivaConL ;
báab%<v%>%<tv%>:báab%>s RegIntransDvT ;
báab%<v%>%<iv%>%<pss%>:báab%>s RegIntransDvTVozPasiva ;
báab%<v%>%<iv%>%<aps%>:báab%>s RegIntransDvTVozAntipasiva ;

! swim
ichkíil%<v%>%<iv%>:ichkíil RegIntransVozActivaSinL ;
ichkíil%<v%>%<tv%>:ichkíil%>t RegIntransDvT ;
ichkíil%<v%>%<iv%>%<pss%>:ichkíil%>t RegIntransDvTVozPasiva ;
ichkíil%<v%>%<iv%>%<aps%>:ichkíil%>t RegIntransDvTVozAntipasiva ;

! raise up/ascend
líik%<v%>%<iv%>:líik RegIntransVozActivaConL ;
líik%<v%>%<tv%>:líik%>s RegIntransDvT ;
líik%<v%>%<iv%>%<pss%>:líik%>s RegIntransDvTVozPasiva ;
líik%<v%>%<iv%>%<aps%>:líik%>s RegIntransDvTVozAntipasiva ;

! raise
naʼak%<v%>%<iv%>:naʼak RegIntransVozActivaConL ;
naʼak%<v%>%<tv%>:naʼak%>s RegIntransDvT ;
naʼak%<v%>%<iv%>%<pss%>:naʼak%>s RegIntransDvTVozPasiva ;
naʼak%<v%>%<iv%>%<aps%>:naʼak%>s RegIntransDvTVozAntipasiva ;

! lower/descend
éem%<v%>%<iv%>:éem RegIntransVozActivaConL ;
éem%<v%>%<tv%>:éem%>s RegIntransDvT ;
éem%<v%>%<iv%>%<pss%>:éem%>s RegIntransDvTVozPasiva ;
éem%<v%>%<iv%>%<aps%>:éem%>s TransVozPasiva ;

! fall
lúub%<v%>%<iv%>:lúub RegIntransVozActivaConL ;
lúub%<v%>%<tv%>:lúubs RegIntransDvT ;
lúub%<v%>%<iv%>%<pss%>:lúub%>s RegIntransDvTVozPasiva ;
lúub%<v%>%<iv%>%<aps%>:lúub%>s RegIntransDvTVozAntipasiva ;

! comer
han%<v%>%<iv%>:han RegIntransVozActivaConL ;
han%<v%>%<tv%>:han%>t RegIntransDvT ;
han%<v%>%<iv%>%<aps%>:han%>t RegIntransDvTVozAntipasiva ;
han%<v%>%<iv%>%<pss%>:han%>t TransVozPasiva ;

! pagar
boʼol%<v%>%<iv%>:boʼol RegIntransVozActivaConL ;
boʼol%<v%>%<tv%>:boʼo%{l%}%>t RegIntransDvT ;
boʼol%<v%>%<iv%>%<pss%>:boʼo%{l%}%>t RegIntransDvTVozPasiva ;

! hacer
bèel%<v%>%<iv%>:bèel RegIntransVozActivaConL ;
bèel%<v%>%<tv%>:bèe%{l%}%>t RegIntransDvT ;
bèel%<v%>%<iv%>%<pss%>:bèe%{l%}%>t RegIntransDvTVozPasiva ;


! contestar, responder
núuk%<v%>%<iv%>:núuk RegIntransVozActivaConL ;
núuk%<v%>%<tv%>:núuk%>t RegIntransDvT ;
núuk%<v%>%<iv%>%<pss%>:núuk%>t RegIntransDvTVozPasiva ;

! entender
naʼat%<v%>%<iv%>:naʼat RegIntransVozActivaConL ;
naʼat%<v%>%<tv%>:naʼat RegIntransDvT ;
naʼat%<v%>%<tv%>:naʼat RegIntransDvT ;
naʼat%<v%>%<iv%>%<pss%>:naʼat RegIntransDvTVozPasiva ;

LEXICON CvcTrans

! Open Question_ Should they lemmatize to the cvc transitive root in all voices?

! paint
bon%<v%>%<tv%>:bon TransVozActiva ;
bon%<v%>%<iv%>%<aps%>:bòon TransVozAntipasiva ;
bon%<v%>%<iv%>%<pss%>:boʼon TransVozPasiva ;
bon%<v%>%<iv%>%<mv%>:bóon TransVozMedia ;

! put
tsʼa%<v%>%<tv%>:tsʼa TransVozActiva ;
tsʼa%<v%>%<iv%>%<aps%>:tsʼàaʼ TransVozAntipasiva ;
! tsʼa%<v%>%<iv%>%<pss%>:tsʼaʼab TransVozPasiva ; TODO: fix

! split apart
buh%<v%>%<tv%>:buh TransVozActiva ;
buh%<v%>%<iv%>%<aps%>:bùuh TransVozAntipasiva ;
buh%<v%>%<iv%>%<pss%>:buʼuh TransVozPasiva ;
buh%<v%>%<iv%>%<mv%>:búuh TransVozMedia ;

! cover, clothe
buk%<v%>%<tv%>:buk TransVozActiva ;
buk%<v%>%<iv%>%<aps%>:bùuk TransVozAntipasiva ;
buk%<v%>%<iv%>%<pss%>:buʼuk TransVozPasiva ;
buk%<v%>%<iv%>%<mv%>:búuk TransVozMedia ;

! sink, submerge
bul%<v%>%<tv%>:bul TransVozActiva ;
bul%<v%>%<iv%>%<aps%>:bùul TransVozAntipasiva ;
bul%<v%>%<iv%>%<pss%>:buʼul TransVozPasiva ;
bul%<v%>%<iv%>%<mv%>:búul TransVozMedia ;

! grab
chʼa%<v%>%<tv%>:chʼa TransVozActiva ;
chʼa%<v%>%<iv%>%<aps%>:chʼaʼ TransVozAntipasiva ;
chʼa%<v%>%<iv%>%<pss%>:chʼaʼab TransVozPasiva ;

! throw, aim
chʼin%<v%>%<tv%>:chʼin TransVozActiva ;
chʼin%<v%>%<iv%>%<aps%>:chʼìin TransVozAntipasiva ;
chʼin%<v%>%<iv%>%<pss%>:chʼiʼin TransVozPasiva ;
chʼin%<v%>%<iv%>%<mv%>:chʼíin TransVozMedia ;

! moisten
chʼul%<v%>%<tv%>:chʼul TransVozActiva ;
chʼul%<v%>%<iv%>%<aps%>:chʼùul TransVozAntipasiva ;
chʼul%<v%>%<iv%>%<pss%>:chʼuʼul TransVozPasiva ;
chʼul%<v%>%<iv%>%<mv%>:chʼúul TransVozMedia ;

! Hang
chʼuy%<v%>%<tv%>:chʼuy TransVozActiva ;
chʼuy%<v%>%<iv%>%<aps%>:chʼùuy TransVozAntipasiva ;
chʼuy%<v%>%<iv%>%<pss%>:chʼuʼuy TransVozPasiva ;
chʼuy%<v%>%<iv%>%<mv%>:chʼúuy TransVozMedia ;

! Catch, fish
chuk%<v%>%<tv%>:chuk TransVozActiva ;
chuk%<v%>%<iv%>%<aps%>:chùuk TransVozAntipasiva ;
chuk%<v%>%<iv%>%<pss%>:chuʼuk TransVozPasiva ;
chuk%<v%>%<iv%>%<mv%>:chúuk TransVozMedia ;

! Extend
hay%<v%>%<tv%>:hay TransVozActiva ;
hay%<v%>%<iv%>%<aps%>:hàay TransVozAntipasiva ;
hay%<v%>%<iv%>%<pss%>:haʼay TransVozPasiva ;
hay%<v%>%<iv%>%<mv%>:háay TransVozMedia ;

! Grind
huchʼ%<v%>%<tv%>:huchʼ TransVozActiva ;
huchʼ%<v%>%<iv%>%<aps%>:hùuchʼ TransVozAntipasiva ;
huchʼ%<v%>%<iv%>%<pss%>:huʼuchʼ TransVozPasiva ;
huchʼ%<v%>%<iv%>%<mv%>:húuchʼ TransVozMedia ;

! sink
hup%<v%>%<tv%>:hup TransVozActiva ;
hup%<v%>%<iv%>%<aps%>:hùup TransVozAntipasiva ;
hup%<v%>%<iv%>%<pss%>:huʼup TransVozPasiva ;
hup%<v%>%<iv%>%<mv%>:húup TransVozMedia ;

! cambiar, mudar
hel%<v%>%<tv%>:hel TransVozActiva ;
hel%<v%>%<iv%>%<aps%>:hèel TransVozAntipasiva ;
hel%<v%>%<iv%>%<pss%>:heʼep TransVozPasiva ;
hel%<v%>%<iv%>%<mv%>:héel TransVozMedia ;

! Change
kʼex%<v%>%<tv%>:kʼex TransVozActiva ;
kʼex%<v%>%<iv%>%<aps%>:kʼèex TransVozAntipasiva ;
kʼex%<v%>%<iv%>%<pss%>:kʼeʼex TransVozPasiva ;
kʼex%<v%>%<iv%>%<mv%>:kʼéex TransVozMedia ;

! sujetar
hakʼ%<v%>%<tv%>:hakʼ TransVozActiva ;
hakʼ%<v%>%<iv%>%<pss%>:haʼakʼ RegIntransDvTVozPasiva ;


! Learn
kan%<v%>%<tv%>:kan TransVozActiva ;
kan%<v%>%<iv%>%<aps%>:kàambal TransVozAntipasiva ;
kan%<v%>%<iv%>%<pss%>:kaʼan TransVozPasiva ;

! Encontrar, localizar
kax%<v%>%<tv%>:kax%>t TransVozActiva ;
kax%<v%>%<iv%>%<pss%>:kax%>t RegIntransDvTVozPasiva ;

! Pull
kol%<v%>%<tv%>:kol TransVozActiva ;
kol%<v%>%<iv%>%<aps%>:kòol TransVozAntipasiva ;
kol%<v%>%<iv%>%<pss%>:koʼol TransVozPasiva ;
kol%<v%>%<iv%>%<mv%>:kóol TransVozMedia ;

! Sell
kon%<v%>%<tv%>:kon TransVozActiva ;
kon%<v%>%<iv%>%<aps%>:kòon TransVozAntipasiva ;
kon%<v%>%<iv%>%<pss%>:koʼon TransVozPasiva ;
kon%<v%>%<iv%>%<mv%>:kóon TransVozMedia ;

! Swallow
lukʼ%<v%>%<tv%>:lukʼ TransVozActiva ;
lukʼ%<v%>%<iv%>%<aps%>:lùukʼ TransVozAntipasiva ;
lukʼ%<v%>%<iv%>%<pss%>:luʼukʼ TransVozPasiva ;
lukʼ%<v%>%<iv%>%<mv%>:lúukʼ TransVozMedia ;

! Grab
machʼ%<v%>%<tv%>:machʼ TransVozActiva ;
machʼ%<v%>%<iv%>%<aps%>:màachʼ TransVozAntipasiva ;
machʼ%<v%>%<iv%>%<pss%>:maʼachʼ TransVozPasiva ;
machʼ%<v%>%<iv%>%<mv%>:máachʼ TransVozMedia ;

! Eat Fast, “stuff-face”
makʼ%<v%>%<tv%>:makʼ TransVozActiva ;
makʼ%<v%>%<iv%>%<aps%>:màakʼ TransVozAntipasiva ;
makʼ%<v%>%<iv%>%<pss%>:maʼakʼ TransVozPasiva ;
makʼ%<v%>%<iv%>%<mv%>:máakʼ TransVozMedia ;

! Buy
man%<v%>%<tv%>:man TransVozActiva ;
man%<v%>%<iv%>%<aps%>:màan TransVozAntipasiva ;
man%<v%>%<iv%>%<pss%>:maʼan TransVozPasiva ;
man%<v%>%<iv%>%<mv%>:máan TransVozMedia ; ! TODO check this

! Bury
muk%<v%>%<tv%>:muk TransVozActiva ;
muk%<v%>%<iv%>%<aps%>:mùuk TransVozAntipasiva ;
muk%<v%>%<iv%>%<pss%>:muʼuk TransVozPasiva ;
muk%<v%>%<iv%>%<mv%>:múuk TransVozMedia ;

! Plant
pʼak%<v%>%<tv%>:pʼak TransVozActiva ;
pʼak%<v%>%<iv%>%<aps%>:pʼàak TransVozAntipasiva ;
pʼak%<v%>%<iv%>%<pss%>:pʼaʼak TransVozPasiva ;
pʼak%<v%>%<iv%>%<mv%>:pʼáak TransVozMedia ;

! dejar
pʼat%<v%>%<tv%>:pʼat TransVozActiva ;
pʼat%<v%>%<iv%>%<aps%>:pʼàat TransVozAntipasiva ;
pʼat%<v%>%<iv%>%<pss%>:pʼaʼat TransVozPasiva ;
pʼat%<v%>%<iv%>%<mv%>:pʼáat TransVozMedia ;

! chip
pʼeh%<v%>%<tv%>:pʼeh TransVozActiva ;
pʼeh%<v%>%<iv%>%<aps%>:pʼèeh TransVozAntipasiva ;
pʼeh%<v%>%<iv%>%<pss%>:pʼeʼeh TransVozPasiva ;
pʼeh%<v%>%<iv%>%<mv%>:pʼéeh TransVozMedia ;


! Open Eyes
pʼil%<v%>%<tv%>:pʼil TransVozActiva ;
pʼil%<v%>%<iv%>%<aps%>:pʼìil TransVozAntipasiva ;
pʼil%<v%>%<iv%>%<pss%>:pʼiʼil TransVozPasiva ;
pʼil%<v%>%<iv%>%<mv%>:pʼíil TransVozMedia ;

! Measure
pʼis%<v%>%<tv%>:pʼis TransVozActiva ;
pʼis%<v%>%<iv%>%<aps%>:pʼìis TransVozAntipasiva ;
pʼis%<v%>%<iv%>%<pss%>:pʼiʼis TransVozPasiva ;
pʼis%<v%>%<iv%>%<mv%>:pʼíis TransVozMedia ;

! Wash
pʼo%<v%>%<tv%>:pʼo TransVozActiva ;
pʼo%<v%>%<iv%>%<aps%>:pʼòo TransVozAntipasiva ;
pʼo%<v%>%<iv%>%<pss%>:pʼoʼo TransVozPasiva ;
pʼo%<v%>%<iv%>%<mv%>:pʼóo TransVozMedia ;

! Insult
pochʼ%<v%>%<tv%>:pochʼ TransVozActiva ;
pochʼ%<v%>%<iv%>%<aps%>:pòochʼ TransVozAntipasiva ;
pochʼ%<v%>%<iv%>%<pss%>:poʼochʼ TransVozPasiva ;
pochʼ%<v%>%<iv%>%<mv%>:póochʼ TransVozMedia ;

! soak
sul%<v%>%<tv%>:sul TransVozActiva ;
sul%<v%>%<iv%>%<aps%>:sùul TransVozAntipasiva ;
sul%<v%>%<iv%>%<pss%>:suʼul TransVozPasiva ;
sul%<v%>%<iv%>%<mv%>:súul TransVozMedia ;

! Girar, voltear
sut%<v%>%<tv%>:sut TransVozActiva ;
sut%<v%>%<iv%>%<aps%>:sùut TransVozAntipasiva ;
sut%<v%>%<iv%>%<pss%>:suʼut TransVozPasiva ;

! Talk, Speak
tʼan%<v%>%<tv%>:tʼan TransVozActiva ;
tʼan%<v%>%<iv%>%<aps%>:tʼàan TransVozAntipasiva ;
tʼan%<v%>%<iv%>%<pss%>:tʼaʼan TransVozPasiva ;
tʼan%<v%>%<iv%>%<mv%>:tʼáan TransVozMedia ;

! Overflow
tʼul%<v%>%<tv%>:tʼul TransVozActiva ;
tʼul%<v%>%<iv%>%<aps%>:tʼùul TransVozAntipasiva ;
tʼul%<v%>%<iv%>%<pss%>:tʼuʼul TransVozPasiva ;
tʼul%<v%>%<iv%>%<mv%>:tʼúul TransVozMedia ;

! Burn (in fire)
tok%<v%>%<tv%>:tok TransVozActiva ;
tok%<v%>%<iv%>%<aps%>:tòok TransVozAntipasiva ;
tok%<v%>%<iv%>%<pss%>:toʼok TransVozPasiva ;
tok%<v%>%<iv%>%<mv%>:tóok TransVozMedia ;

! Fuck 
top%<v%>%<tv%>:top TransVozActiva ;
top%<v%>%<iv%>%<aps%>:tòop TransVozAntipasiva ;
top%<v%>%<iv%>%<pss%>:toʼop TransVozPasiva ;
top%<v%>%<iv%>%<mv%>:tóop TransVozMedia ;

! injertar
tab%<v%>%<tv%>:tab TransVozActiva ;
tab%<v%>%<iv%>%<aps%>:tàab TransVozAntipasiva ;
tab%<v%>%<iv%>%<pss%>:taʼab TransVozPasiva ;
tab%<v%>%<iv%>%<mv%>:táab TransVozMedia ;

! Cure
tsʼak%<v%>%<tv%>:tsʼak TransVozActiva ;
tsʼak%<v%>%<iv%>%<aps%>:tsʼàak TransVozAntipasiva ;
tsʼak%<v%>%<iv%>%<pss%>:tsʼaʼak TransVozPasiva ;
tsʼak%<v%>%<iv%>%<mv%>:tsʼáak TransVozMedia ;

! dar, poner
tsʼaʼ%<v%>%<tv%>:tsʼaʼ TransVozActiva ;
tsʼaʼ%<v%>%<iv%>%<aps%>:tsʼàaʼ TransVozAntipasiva ;
tsʼaʼ%<v%>%<iv%>%<pss%>:tsʼaʼaʼ TransVozPasiva ;


! Peel, Skin
tsol%<v%>%<tv%>:tsol TransVozActiva ;
tsol%<v%>%<iv%>%<aps%>:tsòol TransVozAntipasiva ;
tsol%<v%>%<iv%>%<pss%>:tsoʼol TransVozPasiva ;

! Peel, Skin
tsʼil%<v%>%<tv%>:tsʼil TransVozActiva ;
tsʼil%<v%>%<iv%>%<aps%>:tsʼìil TransVozAntipasiva ;
tsʼil%<v%>%<iv%>%<pss%>:tsʼiʼil TransVozPasiva ;
tsʼil%<v%>%<iv%>%<mv%>:tsʼíil TransVozMedia ;

! Fry
tsa%<v%>%<tv%>:tsa TransVozActiva ;
tsa%<v%>%<iv%>%<aps%>:tsàa TransVozAntipasiva ;
tsa%<v%>%<iv%>%<pss%>:tsaʼa TransVozPasiva ;
tsa%<v%>%<iv%>%<mv%>:tsáa TransVozMedia ;

! Revere/chat
tsik%<v%>%<tv%>:tsik TransVozActiva ;
tsik%<v%>%<iv%>%<aps%>:tsìik TransVozAntipasiva ;
tsik%<v%>%<iv%>%<aps%>:tsiʼik RegIntransVozActivaConL ;
tsik%<v%>%<iv%>%<pss%>:tsiʼik TransVozPasiva ;

! Urinate
wix%<v%>%<tv%>:wix TransVozActiva ;
wix%<v%>%<iv%>%<aps%>:wìix TransVozAntipasiva ;
wix%<v%>%<iv%>%<pss%>:wiʼix TransVozPasiva ;
wix%<v%>%<iv%>%<mv%>:wíix TransVozMedia ;

! Vomit
xeh%<v%>%<tv%>:xeh TransVozActiva ;
xeh%<v%>%<iv%>%<aps%>:xèeh TransVozAntipasiva ;
xeh%<v%>%<iv%>%<pss%>:xeʼeh TransVozPasiva ;
xeh%<v%>%<iv%>%<mv%>:xéeh TransVozMedia ;

! Study, read
xok%<v%>%<tv%>:xok TransVozActiva ;
xok%<v%>%<iv%>%<aps%>:xòok TransVozAntipasiva ;
xok%<v%>%<iv%>%<pss%>:xoʼok TransVozPasiva ;
xok%<v%>%<iv%>%<mv%>:xóok TransVozMedia ;

! Cut
xot%<v%>%<tv%>:xot TransVozActiva ;
xot%<v%>%<iv%>%<aps%>:xòot TransVozAntipasiva ;
xot%<v%>%<iv%>%<pss%>:xoʼot TransVozPasiva ;
xot%<v%>%<iv%>%<mv%>:xóot TransVozMedia ;

! Slice
xotʼ%<v%>%<tv%>:xotʼ TransVozActiva ;
xotʼ%<v%>%<iv%>%<aps%>:xòotʼ TransVozAntipasiva ;
xotʼ%<v%>%<iv%>%<pss%>:xoʼotʼ TransVozPasiva ;
xotʼ%<v%>%<iv%>%<mv%>:xóotʼ TransVozMedia ;

! Consume
xup%<v%>%<tv%>:xup TransVozActiva ;
xup%<v%>%<iv%>%<aps%>:xùup TransVozAntipasiva ;
xup%<v%>%<iv%>%<pss%>:xuʼup TransVozPasiva ;
xup%<v%>%<iv%>%<mv%>:xúup TransVozMedia ;


LEXICON IrregularIntransVerb
xíimbal%<v%>%<iv%>:xíimbal TransVozAntipasiva ; !caminar
xíimbal%<v%>%<tv%>:xíimba%{l%}%>t TransVozActiva ; !visitar

yàan%<v%>%<iv%>:yàan IntransitiveSetB ; ! existir


LEXICON IrregularTransVerb

! to know (conocer)
kʼahóol%<v%>%<tv%>:kʼahóol TransitiveSetB ;
kʼahóol%<v%>%<tv%>:kʼahóo%{l%}%>t RegIntransDvT ;

! to know (saber)
oheel%<v%>%<tv%>:oheel TransitiveSetB ;
oheel%<v%>%<tv%>:ohel TransitiveSetB ;
oheel%<v%>%<tv%>:ohee%{l%}%>t RegIntransDvT ;
oheel%<v%>%<tv%>:ohe%{l%}%>t RegIntransDvT ;

! to know (saber), alternativa
oheel%<v%>%<tv%>:òoheel TransitiveSetB ;
oheel%<v%>%<tv%>:òohel TransitiveSetB ;
oheel%<v%>%<tv%>:òohee%{l%}%>t RegIntransDvT ;
oheel%<v%>%<tv%>:òohe%{l%}%>t RegIntransDvT ;

! see
il%<v%>%<tv%>:il TransVozActiva ;
il%<v%>%<iv%>%<aps%>:ilah TransVozAntipasiva ;
il%<v%>%<iv%>%<pss%>:il IrregularTransVozPasiva ;

! hear/feel
uʼuy%<v%>%<tv%>:uʼuy TransVozActiva ;
uʼuy%<v%>%<iv%>%<aps%>:uʼuyah TransVozAntipasiva ;
uʼuy%<v%>%<iv%>%<pss%>:uʼuy IrregularTransVozPasiva ;

! The ʼbʼ is only used in the 3rd person....should I add the tag for subj_num=3?
uʼub%<v%>%<tv%>:uʼub TransVozActiva ;
uʼub%<v%>%<iv%>%<aps%>:uʼubah TransVozAntipasiva ;
uʼub%<v%>%<iv%>%<pss%>:uʼub IrregularTransVozPasiva ;

! say
aʼal%<v%>%<tv%>:aʼal TransVozActiva ;
aʼal%<v%>%<iv%>%<aps%>:aʼalah TransVozAntipasiva ;
aʼal%<v%>%<iv%>%<pss%>:aʼal IrregularTransVozPasiva ;

! Ask
kʼáat%<v%>%<tv%>:kʼáat TransVozActiva ;
kʼáat%<v%>%<iv%>%<aps%>:kʼáat TransVozAntipasiva ;
kʼáat%<v%>%<iv%>%<pss%>:kʼeáat TransVozPasiva ;


LEXICON PositionalInflections
%<ipfv%>:%>t%>al PluralSubjSuffix ;
%<pfv%>:%>l%>ah IntransitiveSetB ;
%<pf%>:%>l%>ahaʼan IntransitiveSetB ;
%<subj%>:%>l%>ak IntransitiveSetB ;
%<imp%>:%>en IntransitiveSetB ;

LEXICON Positionals
! pucker
bochʼ%<v%>%<pos%>:bochʼ PositionalInflections ;

! stoop, squat, crouch, cower (lower eyes)
motʼ%<v%>%<pos%>:motʼ PositionalInflections ;

! compress, reduce
tsʼan%<v%>%<pos%>:tsʼan PositionalInflections ;

! gather, crease
muchʼ%<v%>%<pos%>:muchʼ PositionalInflections ;

! punch, puncture, bore
tsʼop%<v%>%<pos%>:tsʼop PositionalInflections ;

! pile up, gather (pile up earth, bury)
mul%<v%>%<pos%>:mul PositionalInflections ;

! thrust into a hole
tsʼot%<v%>%<pos%>:tsʼot PositionalInflections ;

! lean against (limit, stop)
nak%<v%>%<pos%>:nak PositionalInflections ;

! sit cross-legged (squash)
tsʼuk%<v%>%<pos%>:tsʼuk PositionalInflections ;

! join, unit
nuch%<v%>%<pos%>:nuch PositionalInflections ;

! bow, grovel (bend)
chin%<v%>%<pos%>:chin PositionalInflections ;

! stare
pʼich%<v%>%<pos%>:pʼich PositionalInflections ;

! pin, fix or set in place
chʼik%<v%>%<pos%>:chʼik PositionalInflections ;

! jump, protrude (jump, leap)
pʼit%<v%>%<pos%>:pʼit PositionalInflections ;

! scatter, stretch out (spread extend)
hay%<v%>%<pos%>:hay PositionalInflections ;

! bend
pʼok%<v%>%<pos%>:pʼok PositionalInflections ;

! straddle, mount (mount)
hek%<v%>%<pos%>:hek PositionalInflections ;

! swell up (swell, inflate, develop)
pʼol%<v%>%<pos%>:pʼol PositionalInflections ;

! sink, insert (insert, encircle)
hup%<v%>%<pos%>:hup PositionalInflections ;

! reside
kah%<v%>%<pos%>:kah PositionalInflections ;

! lean, bend
pʼus%<v%>%<pos%>:pʼus PositionalInflections ;

! place between two (same) things
kap%<v%>%<pos%>:kap PositionalInflections ;

! open, open wide (open, cut open)
xel%<v%>%<pos%>:xel PositionalInflections ;

! roll up, curl up, coil up (roll, twist, wind)
kotsʼ%<v%>%<pos%>:kotsʼ PositionalInflections ;

! open  (open, spread/extend, try for first time; e.g., xeen a xit a wiit/go open your anus for 1st time!)
xit%<v%>%<pos%>:xit PositionalInflections ;

! roll up, twist oneself, fold oneself up (roll, twist, screw)
kop%<v%>%<pos%>:kop PositionalInflections ;

! open, come out, appear (unfold, spread, open as in hand, legs, etc.)
xitʼ%<v%>%<pos%>:xitʼ PositionalInflections ;

! lie athwart, block
kʼat%<v%>%<pos%>:kʼat PositionalInflections ;

! stick to, adhere (fasten, stick to, adhere)
takʼ%<v%>%<pos%>:takʼ PositionalInflections ;

! lean (bend, tilt)
kʼeb%<v%>%<pos%>:kʼeb PositionalInflections ;

! protrude (raise over oneʼs head)
tichʼ%<v%>%<pos%>:tichʼ PositionalInflections ;

! put between two things
lap%<v%>%<pos%>:lap PositionalInflections ;

! bulge, knot (twist, sprain, dislocate)
tukʼ%<v%>%<pos%>:tukʼ PositionalInflections ;

! join, unit
lot%<v%>%<pos%>:lot PositionalInflections ;

! distend, stick out, lie with stomach sticking out (protrude, inflate)
tʼes%<v%>%<pos%>:tʼes PositionalInflections ;

! bend, arch (bend, twist)
met%<v%>%<pos%>:met PositionalInflections ;

! sit
kul%<v%>%<pos%>:kul PositionalInflections ;

! tener vida
kux%<v%>%<pos%>:kux PositionalInflections ;

! expose, extend, tighten (extend, tighten)
tʼin%<v%>%<pos%>:tʼin PositionalInflections ;

! fold hands
moch%<v%>%<pos%>:moch PositionalInflections ;

! ball up, bulge (form ball, make round)
wol%<v%>%<pos%>:wol PositionalInflections ;

! submerge (sink, immerse, dip)
tʼub%<v%>%<pos%>:tʼub PositionalInflections ;

! hang down something gelatinous (drop hand)
tʼoy%<v%>%<pos%>:tʼoy PositionalInflections ;


LEXICON AdjectivalEndings
PluralSubjSuffix ;
Terminatives ;

LEXICON DeadjectivalVerbalEndings
%<iv%>%<ipfv%>:%>t%>al PluralSubjSuffix ;
%<iv%>%<ipfv%>:%>chah%>al PluralSubjSuffix ;
%<iv%>%<pfv%>:%>chah IntransitiveSetB ;
%<iv%>%<pf%>:%>chah%>aʼan IntransitiveSetB ;
%<iv%>%<pf%>:%>chah%>ak IntransitiveSetB ;

LEXICON Interjections
haah%<intj%>:haah # ;
haah%<intj%>:hàah # ;

aah%<intj%>:aah # ;
eeh%<intj%>:eeh # ;
máasima%<intj%>:máasima # ; ! verdad?
òolah%<intj%>:òolah # ; ! hola

ay%<intj%>:ay # ;


LEXICON Adjectives
! good
maʼalob%<adj%>:maʼalob AdjectivalEndings ;
maʼalob%<v%>:maʼalob DeadjectivalVerbalEndings ;


! feliz
kiʼimak%<adj%>:kiʼimak AdjectivalEndings ;

! sick
kohaʼan%<adj%>:kohaʼan AdjectivalEndings ;
kohaʼan%<v%>:kohaʼan DeadjectivalVerbalEndings ;

! good
uts%<adj%>:uts AdjectivalEndings ;
uts%<v%>:uts DeadjectivalVerbalEndings ;

! more
màas%<adj%>:màas AdjectivalEndings ;
màas%<v%>:màas DeadjectivalVerbalEndings ;

! slow, delayed
xàan%<adj%>:xàan AdjectivalEndings ;
xàan%<v%>:xàan DeadjectivalVerbalEndings ;

! existing
yàan%<adj%>:yàan AdjectivalEndings ;
yàan%<v%>:yàan DeadjectivalVerbalEndings ;

! big
nohoch%<adj%>:nohoch AdjectivalEndings ;
nohoch%<v%>:nohoch DeadjectivalVerbalEndings ;

! expensive
koʼoh%<adj%>:koʼoh AdjectivalEndings ;
koʼoh%<v%>:koʼoh DeadjectivalVerbalEndings ;

! possible
páah%<adj%>:páah AdjectivalEndings ;
páah%<v%>:páah DeadjectivalVerbalEndings ;

! town (not an adj...?)
káah%<adj%>:káah AdjectivalEndings ;
káah%<v%>:káah DeadjectivalVerbalEndings ;

! necessary 
kʼabéet%<adj%>:kʼabéet AdjectivalEndings ;
kʼabéet%<v%>:kʼabéet DeadjectivalVerbalEndings ;

! necessary
kʼaʼnaʼan%<adj%>:kʼaʼanaʼan AdjectivalEndings ;

! mucho
yaʼab%<adj%>:yaʼab AdjectivalEndings ;
yaʼab%<v%>:yaʼab DeadjectivalVerbalEndings ;

! fuerte, recio
kʼaʼam%<adj%>:kʼaʼam AdjectivalEndings ;
kʼaʼam%<v%>:kʼaʼam DeadjectivalVerbalEndings ;

! hermos@
kiʼichpam%<adj%>:kiʼichpam AdjectivalEndings ;
kiʼichpam%<v%>:kiʼichpam DeadjectivalVerbalEndings ;

