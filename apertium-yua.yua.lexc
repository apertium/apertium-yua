!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
Multichar_Symbols

%{V%}
%{l%}
%<v%>    ! Verbo

%<n%>    ! Sustantivo
%<adj%>  ! Adjetivo
%<pl%>   ! Plural
%<s_pl%>   ! Sujeto Plural

%<prx%>  ! Proximal ʼthis _ʼ
%<med%>  ! Medial ʼthat _ʼ
%<dst%>  ! Distal ʼthat _ over thereʼ

%<incomp%> ! "ongoing incomplete"
%<past%> ! "Completed simple past"
%<perf%> ! "Completed Present Perfect"
%<subjun%> ! "Subjunctive"
%<pron%>  ! "pronoun"
%<1sg_poss%>  ! "pronoun"
%<2sg_poss%>  ! "pronoun"
%<3sg_poss%>  ! "pronoun"
%<1pl_poss%>  ! "pronoun"
%<2pl_poss%>  ! "pronoun"
%<3pl_poss%>  ! "pronoun"

%<poss_nonownership%>
%<poss_comm%>
%<poss_internal%>

%<poss%>  ! "posesivo"
%<itg%>

%<clfanim%>
%<clfinan%>

%<hab%>

%<neg%>

%<ipfv%> ! imperfectivo "Ongoing incomplete"
%<pfv%>  ! perfectivo "Simple Past"
%<pf%>   ! perfect "Completed Present"
%<subj%>  ! subjunctive
%<s_sg%> ! sujeto singular
%<imp%>  ! imperativo
%<s_pl%> ! sujeto plural

%<s_sg1%>
%<s_sg2%>
%<s_sg3%>
%<s_pl1%>
%<s_pl2%>
%<s_pl3%>

%<o_sg1%>
%<o_sg2%>
%<o_sg3%>
%<o_pl1%>
%<o_pl2%>
%<o_pl3%>

%<pron_1sg%>
%<pron_2sg%>
%<pron_3sg%>
%<pron_1pl%>
%<pron_2pl%>
%<pron_3pl%>

%<female_agentive%>
%<male_agentive%>

%<tv%>            !
%<iv%>            ! 

%<attr%>          ! Attributive
%<der_l%>         ! Adjective derivation
%<term%>          ! Terminative morpheme
%<top%>          ! Topicalized
%<loc%>           ! locative

%<pos%> ! positional

%<num%>

%<hab%>
%<dur%>
%<cont%>
%<cess%>
%<incep%>
%<des%>


%{o%}             ! Either ʼoʼ or nothing
%{e%}             ! Either ʼeʼ or nothing
%{V%}             ! Copy stem vowel
%{wy%}          ! start to vowel-initial verb stems either y- or w- depending on person of subject.

ʼ                ! Glottal, U+02BC
%>                ! Morpheme boundary

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root
VerbandNounInit ;
SetA ;
PronombresIndependientes ;
Negative ;
AspectMarker ;
Adjectives ;
NominalClassifiers ;
Numbers ;
Interrogativos ;

LEXICON VerbandNounInit

! TODO: flag diacritics so that these only happen in the imperfective,
! and w only 1st/2nd persons, y only 3rd persons
0:w VerborPossessiveNoun ;
0:y VerborPossessiveNoun ;
VerborPossessiveNoun ;

LEXICON VerborPossessiveNoun
CvcTrans ;
IntransReg ;
IrregularTransVerb ;
IrregularIntransVerb ;
Positionals ;
Noun ;

LEXICON AspectMarker
ImperfectiveAspectMarker ;
PerfectiveAspectMarker ;

LEXICON ImperfectiveAspectMarker
%<hab%>:k # ;
%<hab%>%<neg%>:t # ;
%<dur%>:táan # ;
%<cont%>:lailiʼ # ;
%<cess%>:tsʼoʼok # ;
%<cess%>:taant # ; ! how to distinguish this proximate terminative?
%<incep%>:hoʼop # ;
%<incep%>:kaah # ;
%<incep%>:chuun # ;
%<incep%>:lek # ;
%<des%>:taak # ;

LEXICON PerfectiveAspectMarker
%<pfv%>%<tv%>:t # ;

%<s_sg1%>%<pron%>%<pfv%>%<tv%>:tin # ;
%<s_sg2%>%<pron%>%<pfv%>%<tv%>:ta # ;

! TODO: is this accurate? verify t + u
%<s_sg3%>%<pron%>%<pfv%>%<tv%>:tu # ; 

%<pfv%>%<iv%>:h # ;


LEXICON Negative
%<neg%>:maʼa # ;
%<hab%>%<neg%>:maʼat # ;
%<neg%>:ma # ;



LEXICON SetA
%<s_sg1%>%<pron%>:in # ;
%<s_sg1%>%<pron%>%<hab%>:kin # ;
%<s_sg1%>%<pron%>%<dur%>:tin # ;

%<s_sg2%>%<pron%>:a # ;
%<s_sg2%>%<pron%>%<hab%>:ka # ;
%<s_sg2%>%<pron%>%<dur%>:tan # ;


%<s_sg3%>%<pron%>:u # ;
%<s_sg3%>%<pron%>%<hab%>:ku # ;

%<s_pl1%>%<pron%>:k # ;

%<s_pl2%>%<pron%>:a # ;
%<s_pl2%>%<pron%>%<hab%>:ka # ;
%<s_pl2%>%<pron%>%<dur%>:tan # ;
%<s_pl2%>%<pron%>%<pfv%>:ta # ;

%<s_pl3%>%<pron%>:u # ;


%<1sg_poss%>%<pron%>:in # ;
%<2sg_poss%>%<pron%>:a # ;
%<3sg_poss%>%<pron%>:u # ;
%<1pl_poss%>%<pron%>:k # ;
%<2pl_poss%>%<pron%>:a # ;
%<3pl_poss%>%<pron%>:u # ;

LEXICON PronombresIndependientes
%<pron_1sg%>:téen Topicalizer ;
%<pron_2sg%>:téech Topicalizer ;
%<pron_3sg%>:letiʼ Topicalizer ;

%<pron_1pl%>:toʼon Topicalizer ;
%<pron_2pl%>:teʼex Topicalizer ;
%<pron_3pl%>:letiʼ%>oʼob Topicalizer ;

LEXICON Topicalizer
%<top%>:eʼ # ;

Terminatives ;

LEXICON TransitiveSetB
%<o_sg1%>:en%> PluralSubjSuffix ;
%<o_pl1%>:oʼon%> PluralSubjSuffix ;
%<o_sg2%>:ech%> PluralSubjSuffix ;
%<o_pl2%>:eʼex%> PluralSubjSuffix ;
%<o_sg1%>:en%> PluralSubjSuffix ;
%<o_sg3%>:0%> PluralSubjSuffix ;
%<o_sg3%>:ih%> PluralSubjSuffix ;
%<o_pl3%>:oʼob%> PluralSubjSuffix ;
Reflexives ;

LEXICON Reflexives
%<refl_1sg%>:inbah%> Terminatives ;
%<refl_2sg%>:abah%> Terminatives ;
%<refl_3sg%>:ubah%> Terminatives ;
%<refl_1pl%>:kbah%> Terminatives ;
%<refl_2pl%>:abaheʼex%> Terminatives ;
%<refl_3pl%>:ubahoʼob%> Terminatives ;

LEXICON IntransitiveSetB
%<s_sg1%>:en Terminatives ;
%<s_pl1%>:oʼon Terminatives ;
%<s_sg2%>:ech Terminatives ;
%<s_pl2%>:eʼex Terminatives ;
%<s_sg1%>:en Terminatives ;
%<s_sg3%>:0 Terminatives ;
%<s_pl3%>:oʼob Terminatives ;

LEXICON PluralSubjSuffix
%<s_pl%>:oʼob%> Terminatives ;
%<s_pl%>:eʼex%> Terminatives ;
Terminatives ;

LEXICON Terminatives
%<neg%>:iʼ # ;
%<loc%>:iʼ # ;

! these are endings to the aspect markers laili and taant, respectively
%<cont%>:eʼ # ; 
%<cess%>:eʼ # ;
%<top%>:eʼ # ;


# ;

LEXICON RegIntransVozActivaSinL
%<ipfv%>:0%> IntransitiveSetB;
%<pfv%>:0%> IntransitiveSetB;
%<pf%>:ahaʼan%> IntransitiveSetB;
%<subj%>:%{V%}k%> IntransitiveSetB;
%<imp%>%<s_2sg%>:nen%> Terminatives;
%<imp%>%<s_2pl%>:neneʼex%> Terminatives;

LEXICON RegIntransVozActivaConL
%<ipfv%>:%{V%}l%> PluralSubjSuffix;
%<pfv%>:0%> IntransitiveSetB;
%<pf%>:ahaʼan%> IntransitiveSetB;
%<subj%>:%{V%}k%> IntransitiveSetB;
%<imp%>%<s_2sg%>:nen%> Terminatives;
%<imp%>%<s_2pl%>:neneʼex%> Terminatives;

LEXICON RegIntransDvT
%<ipfv%>:ik%> TransitiveSetB;
%<pfv%>:ah%> TransitiveSetB;
%<pf%>:mah%> TransitiveSetB;
%<subj%>:0%> TransitiveSetB;
%<imp%>%<s_2sg%>:eh%> Terminatives;
%<imp%>%<s_2sg%>:0%> Terminatives;
%<imp%>%<s_2pl%>:0%> PluralSubjSuffix;

LEXICON RegIntransDvTVozPasiva
%<ipfv%>:aʼal%> IntransitiveSetB;
%<pfv%>:aʼab%> IntransitiveSetB;
%<pf%>:aʼan%> IntransitiveSetB;
%<subj%>:aʼak%> IntransitiveSetB;

! TODO: check on this....whatʼs the plural ending?
%<imp%>:aʼak%> Terminatives;

LEXICON RegIntransDvTVozAntipasiva

%<ipfv%>:ah%> IntransitiveSetB;
%<pfv%>:ahnah%> IntransitiveSetB;
%<pf%>:ahnahaʼan%> IntransitiveSetB;
%<subj%>:ahnak%> IntransitiveSetB;

! TODO: check on this....whatʼs the plural ending?
%<imp%>:ahnak%> Terminatives;

LEXICON TransVozActiva
%<ipfv%>:ik%> TransitiveSetB;
%<pfv%>:ah%> TransitiveSetB;
%<pf%>:mah%> TransitiveSetB;
%<subj%>:0%> TransitiveSetB;
%<subj%>:eh%> TransitiveSetB;
%<imp%>:0%> TransitiveSetB;
%<imp%>:eh%> TransitiveSetB;


LEXICON TransVozAntipasiva
%<ipfv%>:0%> PluralSubjSuffix;

! TODO: for all of the following entries, the geminate "n%>n -> n"
%<pfv%>:nah%> IntransitiveSetB;
%<pf%>:nahaʼan%> IntransitiveSetB;
%<subj%>%<s_sg%>:nak%> IntransitiveSetB;
%<imp%>:nen%> IntransitiveSetB;

LEXICON TransVozPasiva
%<ipfv%>:%{V%}l%> PluralSubjSuffix;
%<pfv%>:aʼab%> IntransitiveSetB;
%<pf%>:aʼan%> IntransitiveSetB;
%<subj%>:%{V%}k%> IntransitiveSetB;
! %<imp%>:%{V%}k%> IntransitiveSetB;  ! this is kind of weird, eg "be loved!" TODO verify that it is a valid form

LEXICON IrregularTransVozPasiva
%<ipfv%>:aʼal%> PluralSubjSuffix;
%<pfv%>:aʼab%> IntransitiveSetB;
%<pf%>:aʼan%> IntransitiveSetB;
%<subj%>:aʼak%> IntransitiveSetB;
! %<imp%>:aʼak%> IntransitiveSetB;

LEXICON TransVozMedia
%<ipfv%>:%{V%}l%> PluralSubjSuffix;
%<pfv%>:0%> IntransitiveSetB;
%<pf%>:aʼan%> IntransitiveSetB;
%<subj%>:%{V%}k%> IntransitiveSetB;
%<imp%>:0%> IntransitiveSetB;


LEXICON PossessiveNominalSuffixes
%<poss_comm%>:il IntransitiveSetB ;
%<poss_nonownership%>:al IntransitiveSetB ;
%<poss_internal%>:el IntransitiveSetB ;
PluralSuffix ;
IntransitiveSetB ;

LEXICON PluralSuffix
%<pl%>:oʼob Terminatives ;

LEXICON NounEndings
Terminatives ;
PluralSuffix ;
IntransitiveSetB ;
PossessiveNominalSuffixes ;

LEXICON NominalClassifiers
%<clfanim%>:tuul # ;
%<clfinan%>:pʼéel # ;
%<clfinan%>%<num%>:humpʼéel # ;
%<clfanim%>%<num%>:huntuul # ;

LEXICON Numbers
%<num%>:hun # ;
%<num%>:kaʼa # ;
%<num%>:ox # ;
%<num%>:kam # ;
%<num%>:hó # ;

LEXICON TerminacionesInterrogativas
0:ih # ;
# ;

LEXICON Interrogativos
%<pron%>%<itg%>:baʼax TerminacionesInterrogativas ;
%<adv%>%<itg%>:bix TerminacionesInterrogativas ;

LEXICON Noun
tsíimin%<n%>:tsíimin NounEndings ; ! "caballo"
káambal%<n%>:káambal NounEndings ; ! "estudiante"
kaʼansah%<n%>:kaʼansah NounEndings ; ! "maestro"
kòolnáal%<n%>:kòolnáal NounEndings ; ! "agricultor(a)"
wíinik%<n%>:wíinik NounEndings ; ! "humano"
máak%<n%>:máak NounEndings ; ! "persona"
míis%<n%>:míis NounEndings ; ! "gato"
pèek%<n%>:pèek NounEndings ; ! "perro"
huʼunalte%<n%>:huʼunalte NounEndings ; ! "libro"
ùulum%<n%>:ùulum NounEndings ; ! "huajolote"
bàatsil%<n%>:bàatsil NounEndings ; ! "amigo"
kʼàaba%<n%>:kʼàaba NounEndings ; ! "nombre"
ich%<n%>:ich NounEndings ; ! "ojo"


LEXICON IntransReg
! go
bin%<v%>%<iv%>:bin%> RegIntransVozActivaSinL ;
bin%<v%>%<tv%>:bis%> RegIntransDvT ;
bin%<v%>%<iv%>%<pss%>:bins%> RegIntransDvTVozPasiva ;
bin%<v%>%<iv%>%<aps%>:bins%> RegIntransDvTVozAntipasiva ;

! arrive
tal%<v%>%<iv%>:tal%> RegIntransVozActivaConL ;
tal%<v%>%<tv%>:tas%> RegIntransDvT ;
tal%<v%>%<iv%>%<pss%>:tas%> RegIntransDvTVozPasiva ;
tal%<v%>%<iv%>%<aps%>:tas%> RegIntransDvTVozAntipasiva ;

! go about (pasear)
máan%<v%>%<iv%>:máan%> RegIntransVozActivaConL ;
máan%<v%>%<tv%>:máans%> RegIntransDvT ;
máan%<v%>%<iv%>%<pss%>:máans%> RegIntransDvTVozPasiva ;
máan%<v%>%<iv%>%<aps%>:máans%> RegIntransDvTVozAntipasiva ;

! sleep
wen%<v%>%<iv%>:wen%> RegIntransVozActivaConL ;
wen%<v%>%<tv%>:wens%> RegIntransDvT ;
wen%<v%>%<iv%>%<pss%>:wens%> RegIntransDvTVozPasiva ;
wen%<v%>%<iv%>%<aps%>:wens%> RegIntransDvTVozAntipasiva ;

! wake up
ah%<v%>%<iv%>:%{wy%}ah%> RegIntransVozActivaConL ;
ah%<v%>%<tv%>:%{wy%}ahs%> RegIntransDvT ;
ah%<v%>%<iv%>%<pss%>:%{wy%}ahs%> RegIntransDvTVozPasiva ;
ah%<v%>%<iv%>%<aps%>:%{wy%}ahs%> RegIntransDvTVozAntipasiva ;

! finish, to be done
tsʼoʼok%<v%>%<iv%>:tsʼoʼok%> RegIntransVozActivaConL ;
tsʼoʼok%<v%>%<tv%>:tsʼoʼoks%> RegIntransDvT ;
tsʼoʼok%<v%>%<iv%>%<pss%>:tsʼoʼoks%> RegIntransDvTVozPasiva ;
tsʼoʼok%<v%>%<iv%>%<aps%>:tsʼoʼoks%> RegIntransDvTVozAntipasiva ;

! reply
núuk%<v%>%<iv%>:núuk%> RegIntransVozActivaConL ;
núuk%<v%>%<tv%>:núuk%> RegIntransDvT ;
núuk%<v%>%<iv%>%<pss%>:núuks%> RegIntransDvTVozPasiva ;
núuk%<v%>%<iv%>%<aps%>:núuks%> RegIntransDvTVozAntipasiva ;

! enter
ok%<v%>%<iv%>:ok%> RegIntransVozActivaConL ;
ok%<v%>%<tv%>:oks%> RegIntransDvT ;
ok%<v%>%<iv%>%<pss%>:oks%> RegIntransDvTVozPasiva ;
ok%<v%>%<iv%>%<aps%>:oks%> RegIntransDvTVozAntipasiva ;
ok%<v%>%<iv%>%<aps%>:óok%> RegIntransDvTVozAntipasiva ;

! exit
hóok%<v%>%<iv%>:hóok%> RegIntransVozActivaConL ;
hóok%<v%>%<tv%>:hóoks%> RegIntransDvT ;
hóok%<v%>%<iv%>%<pss%>:hóoks%> RegIntransDvTVozPasiva ;
hóok%<v%>%<iv%>%<aps%>:hóoks%> RegIntransDvTVozAntipasiva ;

! swim
báab%<v%>%<iv%>:báab%> RegIntransVozActivaConL ;
báab%<v%>%<tv%>:báabs%> RegIntransDvT ;
báab%<v%>%<iv%>%<pss%>:báabs%> RegIntransDvTVozPasiva ;
báab%<v%>%<iv%>%<aps%>:báabs%> RegIntransDvTVozAntipasiva ;

! swim
ichkíil%<v%>%<iv%>:ichkíil%> RegIntransVozActivaSinL ;
ichkíil%<v%>%<tv%>:ichkíilt%> RegIntransDvT ;
ichkíil%<v%>%<iv%>%<pss%>:ichkíilt%> RegIntransDvTVozPasiva ;
ichkíil%<v%>%<iv%>%<aps%>:ichkíilt%> RegIntransDvTVozAntipasiva ;

! raise up/ascend
líik%<v%>%<iv%>:líik%> RegIntransVozActivaConL ;
líik%<v%>%<tv%>:líiks%> RegIntransDvT ;
líik%<v%>%<iv%>%<pss%>:líiks%> RegIntransDvTVozPasiva ;
líik%<v%>%<iv%>%<aps%>:líiks%> RegIntransDvTVozAntipasiva ;

! lower/descend
éem%<v%>%<iv%>:éem%> RegIntransVozActivaConL ;
éem%<v%>%<tv%>:éems%> RegIntransDvT ;
éem%<v%>%<iv%>%<pss%>:éems%> RegIntransDvTVozPasiva ;
éem%<v%>%<iv%>%<aps%>:éems%> RegIntransDvTVozAntipasiva ;

! fall
lúub%<v%>%<iv%>:lúub%> RegIntransVozActivaConL ;
lúub%<v%>%<tv%>:lúubs%> RegIntransDvT ;
lúub%<v%>%<iv%>%<pss%>:lúubs%> RegIntransDvTVozPasiva ;
lúub%<v%>%<iv%>%<aps%>:lúubs%> RegIntransDvTVozAntipasiva ;


! comer
han%<v%>%<iv%>:han%> RegIntransVozActivaConL ;
han%<v%>%<tv%>:hant%> RegIntransDvT ;
han%<v%>%<iv%>%<pss%>:hant%> RegIntransDvTVozPasiva ;


LEXICON CvcTrans

! Open Question_ Should they lemmatize to the cvc transitive root in all voices?

! paint
bon%<v%>%<tv%>:bon%> TransVozActiva ;
bon%<v%>%<iv%>%<aps%>:bòon%> TransVozAntipasiva ;
bon%<v%>%<iv%>%<pss%>:boʼon%> TransVozPasiva ;
bon%<v%>%<iv%>%<mv%>:bóon%> TransVozMedia ;

! split apart
buh%<v%>%<tv%>:buh%> TransVozActiva ;
buh%<v%>%<iv%>%<aps%>:bùuh%> TransVozAntipasiva ;
buh%<v%>%<iv%>%<pss%>:buʼuh%> TransVozPasiva ;
buh%<v%>%<iv%>%<mv%>:búuh%> TransVozMedia ;

! cover, clothe
buk%<v%>%<tv%>:buk%> TransVozActiva ;
buk%<v%>%<iv%>%<aps%>:bùuk%> TransVozAntipasiva ;
buk%<v%>%<iv%>%<pss%>:buʼuk%> TransVozPasiva ;
buk%<v%>%<iv%>%<mv%>:búuk%> TransVozMedia ;

! sink, submerge
bul%<v%>%<tv%>:bul%> TransVozActiva ;
bul%<v%>%<iv%>%<aps%>:bùul%> TransVozAntipasiva ;
bul%<v%>%<iv%>%<pss%>:buʼul%> TransVozPasiva ;
bul%<v%>%<iv%>%<mv%>:búul%> TransVozMedia ;

! throw, aim
chʼin%<v%>%<tv%>:chʼin%> TransVozActiva ;
chʼin%<v%>%<iv%>%<aps%>:chʼìin%> TransVozAntipasiva ;
chʼin%<v%>%<iv%>%<pss%>:chʼiʼin%> TransVozPasiva ;
chʼin%<v%>%<iv%>%<mv%>:chʼíin%> TransVozMedia ;

! moisten
chʼul%<v%>%<tv%>:chʼul%> TransVozActiva ;
chʼul%<v%>%<iv%>%<aps%>:chʼùul%> TransVozAntipasiva ;
chʼul%<v%>%<iv%>%<pss%>:chʼuʼul%> TransVozPasiva ;
chʼul%<v%>%<iv%>%<mv%>:chʼúul%> TransVozMedia ;

! Hang
chʼuy%<v%>%<tv%>:chʼuy%> TransVozActiva ;
chʼuy%<v%>%<iv%>%<aps%>:chʼùuy%> TransVozAntipasiva ;
chʼuy%<v%>%<iv%>%<pss%>:chʼuʼuy%> TransVozPasiva ;
chʼuy%<v%>%<iv%>%<mv%>:chʼúuy%> TransVozMedia ;

! Catch, fish
chuk%<v%>%<tv%>:chuk%> TransVozActiva ;
chuk%<v%>%<iv%>%<aps%>:chùuk%> TransVozAntipasiva ;
chuk%<v%>%<iv%>%<pss%>:chuʼuk%> TransVozPasiva ;
chuk%<v%>%<iv%>%<mv%>:chúuk%> TransVozMedia ;

! Extend
hay%<v%>%<tv%>:hay%> TransVozActiva ;
hay%<v%>%<iv%>%<aps%>:háay%> TransVozAntipasiva ;
hay%<v%>%<iv%>%<pss%>:haʼay%> TransVozPasiva ;
hay%<v%>%<iv%>%<mv%>:hàay%> TransVozMedia ;

! Grind
huchʼ%<v%>%<tv%>:huchʼ%> TransVozActiva ;
huchʼ%<v%>%<iv%>%<aps%>:húuchʼ%> TransVozAntipasiva ;
huchʼ%<v%>%<iv%>%<pss%>:huʼuchʼ%> TransVozPasiva ;
huchʼ%<v%>%<iv%>%<mv%>:hùuchʼ%> TransVozMedia ;

! sink
hup%<v%>%<tv%>:hup%> TransVozActiva ;
hup%<v%>%<iv%>%<aps%>:húup%> TransVozAntipasiva ;
hup%<v%>%<iv%>%<pss%>:huʼup%> TransVozPasiva ;
hup%<v%>%<iv%>%<mv%>:hùup%> TransVozMedia ;


! Change
kʼex%<v%>%<tv%>:kʼex%> TransVozActiva ;
kʼex%<v%>%<iv%>%<aps%>:kʼéex%> TransVozAntipasiva ;
kʼex%<v%>%<iv%>%<pss%>:kʼeʼex%> TransVozPasiva ;
kʼex%<v%>%<iv%>%<mv%>:kʼèex%> TransVozMedia ;

! Learn
kan%<v%>%<tv%>:kan%> TransVozActiva ;
kan%<v%>%<iv%>%<aps%>:káan%> TransVozAntipasiva ;
kan%<v%>%<iv%>%<pss%>:kaʼan%> TransVozPasiva ;
kan%<v%>%<iv%>%<mv%>:kàan%> TransVozMedia ;

! Pull
kol%<v%>%<tv%>:kol%> TransVozActiva ;
kol%<v%>%<iv%>%<aps%>:kóol%> TransVozAntipasiva ;
kol%<v%>%<iv%>%<pss%>:koʼol%> TransVozPasiva ;
kol%<v%>%<iv%>%<mv%>:kòol%> TransVozMedia ;

! Sell
kon%<v%>%<tv%>:kon%> TransVozActiva ;
kon%<v%>%<iv%>%<aps%>:kóon%> TransVozAntipasiva ;
kon%<v%>%<iv%>%<pss%>:koʼon%> TransVozPasiva ;
kon%<v%>%<iv%>%<mv%>:kòon%> TransVozMedia ;

! Swallow
lukʼ%<v%>%<tv%>:lukʼ%> TransVozActiva ;
lukʼ%<v%>%<iv%>%<aps%>:lúukʼ%> TransVozAntipasiva ;
lukʼ%<v%>%<iv%>%<pss%>:luʼukʼ%> TransVozPasiva ;
lukʼ%<v%>%<iv%>%<mv%>:lùukʼ%> TransVozMedia ;

! Grab
machʼ%<v%>%<tv%>:machʼ%> TransVozActiva ;
machʼ%<v%>%<iv%>%<aps%>:máachʼ%> TransVozAntipasiva ;
machʼ%<v%>%<iv%>%<pss%>:maʼachʼ%> TransVozPasiva ;
machʼ%<v%>%<iv%>%<mv%>:màachʼ%> TransVozMedia ;

! Eat Fast, “stuff-face”
makʼ%<v%>%<tv%>:makʼ%> TransVozActiva ;
makʼ%<v%>%<iv%>%<aps%>:máakʼ%> TransVozAntipasiva ;
makʼ%<v%>%<iv%>%<pss%>:maʼakʼ%> TransVozPasiva ;
makʼ%<v%>%<iv%>%<mv%>:màakʼ%> TransVozMedia ;

! Buy
man%<v%>%<tv%>:man%> TransVozActiva ;
man%<v%>%<iv%>%<aps%>:màan%> TransVozAntipasiva ;
man%<v%>%<iv%>%<pss%>:maʼan%> TransVozPasiva ;
man%<v%>%<iv%>%<mv%>:màan%> TransVozMedia ; ! TODO check this

! Bury
muk%<v%>%<tv%>:muk%> TransVozActiva ;
muk%<v%>%<iv%>%<aps%>:múuk%> TransVozAntipasiva ;
muk%<v%>%<iv%>%<pss%>:muʼuk%> TransVozPasiva ;
muk%<v%>%<iv%>%<mv%>:mùuk%> TransVozMedia ;

! Plant
pʼak%<v%>%<tv%>:pʼak%> TransVozActiva ;
pʼak%<v%>%<iv%>%<aps%>:pʼáak%> TransVozAntipasiva ;
pʼak%<v%>%<iv%>%<pss%>:pʼaʼak%> TransVozPasiva ;
pʼak%<v%>%<iv%>%<mv%>:pʼàak%> TransVozMedia ;

! Open Eyes
pʼil%<v%>%<tv%>:pʼil%> TransVozActiva ;
pʼil%<v%>%<iv%>%<aps%>:pʼíil%> TransVozAntipasiva ;
pʼil%<v%>%<iv%>%<pss%>:pʼiʼil%> TransVozPasiva ;
pʼil%<v%>%<iv%>%<mv%>:pʼìil%> TransVozMedia ;

! Measure
pʼis%<v%>%<tv%>:pʼis%> TransVozActiva ;
pʼis%<v%>%<iv%>%<aps%>:pʼíis%> TransVozAntipasiva ;
pʼis%<v%>%<iv%>%<pss%>:pʼiʼis%> TransVozPasiva ;
pʼis%<v%>%<iv%>%<mv%>:pʼìis%> TransVozMedia ;

! Wash
pʼo%<v%>%<tv%>:pʼo%> TransVozActiva ;
pʼo%<v%>%<iv%>%<aps%>:pʼóo%> TransVozAntipasiva ;
pʼo%<v%>%<iv%>%<pss%>:pʼoʼo%> TransVozPasiva ;
pʼo%<v%>%<iv%>%<mv%>:pʼòo%> TransVozMedia ;

! Insult
pochʼ%<v%>%<tv%>:pochʼ%> TransVozActiva ;
pochʼ%<v%>%<iv%>%<aps%>:póochʼ%> TransVozAntipasiva ;
pochʼ%<v%>%<iv%>%<pss%>:poʼochʼ%> TransVozPasiva ;
pochʼ%<v%>%<iv%>%<mv%>:pòochʼ%> TransVozMedia ;

! Talk, Speak
tʼan%<v%>%<tv%>:tʼan%> TransVozActiva ;
tʼan%<v%>%<iv%>%<aps%>:tʼáan%> TransVozAntipasiva ;
tʼan%<v%>%<iv%>%<pss%>:tʼaʼan%> TransVozPasiva ;
tʼan%<v%>%<iv%>%<mv%>:tʼàan%> TransVozMedia ;

! Overflow
tʼul%<v%>%<tv%>:tʼul%> TransVozActiva ;
tʼul%<v%>%<iv%>%<aps%>:tʼúul%> TransVozAntipasiva ;
tʼul%<v%>%<iv%>%<pss%>:tʼuʼul%> TransVozPasiva ;
tʼul%<v%>%<iv%>%<mv%>:tʼùul%> TransVozMedia ;

! Burn (in fire)
tok%<v%>%<tv%>:tok%> TransVozActiva ;
tok%<v%>%<iv%>%<aps%>:tóok%> TransVozAntipasiva ;
tok%<v%>%<iv%>%<pss%>:toʼok%> TransVozPasiva ;
tok%<v%>%<iv%>%<mv%>:tòok%> TransVozMedia ;

! Fuck 
top%<v%>%<tv%>:top%> TransVozActiva ;
top%<v%>%<iv%>%<aps%>:tóop%> TransVozAntipasiva ;
top%<v%>%<iv%>%<pss%>:toʼop%> TransVozPasiva ;
top%<v%>%<iv%>%<mv%>:tòop%> TransVozMedia ;

! Cure
tsʼak%<v%>%<tv%>:tsʼak%> TransVozActiva ;
tsʼak%<v%>%<iv%>%<aps%>:tsʼáak%> TransVozAntipasiva ;
tsʼak%<v%>%<iv%>%<pss%>:tsʼaʼak%> TransVozPasiva ;
tsʼak%<v%>%<iv%>%<mv%>:tsʼàak%> TransVozMedia ;

! Peel, Skin
tsʼil%<v%>%<tv%>:tsʼil%> TransVozActiva ;
tsʼil%<v%>%<iv%>%<aps%>:tsʼíil%> TransVozAntipasiva ;
tsʼil%<v%>%<iv%>%<pss%>:tsʼiʼil%> TransVozPasiva ;
tsʼil%<v%>%<iv%>%<mv%>:tsʼìil%> TransVozMedia ;

! Fry
tsa%<v%>%<tv%>:tsa%> TransVozActiva ;
tsa%<v%>%<iv%>%<aps%>:tsáa%> TransVozAntipasiva ;
tsa%<v%>%<iv%>%<pss%>:tsaʼa%> TransVozPasiva ;
tsa%<v%>%<iv%>%<mv%>:tsàa%> TransVozMedia ;

! Urinate
wix%<v%>%<tv%>:wix%> TransVozActiva ;
wix%<v%>%<iv%>%<aps%>:wíix%> TransVozAntipasiva ;
wix%<v%>%<iv%>%<pss%>:wiʼix%> TransVozPasiva ;
wix%<v%>%<iv%>%<mv%>:wìix%> TransVozMedia ;

! Vomit
xeh%<v%>%<tv%>:xeh%> TransVozActiva ;
xeh%<v%>%<iv%>%<aps%>:xéeh%> TransVozAntipasiva ;
xeh%<v%>%<iv%>%<pss%>:xeʼeh%> TransVozPasiva ;
xeh%<v%>%<iv%>%<mv%>:xèeh%> TransVozMedia ;

! Study, read
xok%<v%>%<tv%>:xok%> TransVozActiva ;
xok%<v%>%<iv%>%<aps%>:xóok%> TransVozAntipasiva ;
xok%<v%>%<iv%>%<pss%>:xoʼok%> TransVozPasiva ;
xok%<v%>%<iv%>%<mv%>:xòok%> TransVozMedia ;

! Cut
xot%<v%>%<tv%>:xot%> TransVozActiva ;
xot%<v%>%<iv%>%<aps%>:xóot%> TransVozAntipasiva ;
xot%<v%>%<iv%>%<pss%>:xoʼot%> TransVozPasiva ;
xot%<v%>%<iv%>%<mv%>:xòot%> TransVozMedia ;

! Slice
xotʼ%<v%>%<tv%>:xotʼ%> TransVozActiva ;
xotʼ%<v%>%<iv%>%<aps%>:xóotʼ%> TransVozAntipasiva ;
xotʼ%<v%>%<iv%>%<pss%>:xoʼotʼ%> TransVozPasiva ;
xotʼ%<v%>%<iv%>%<mv%>:xòotʼ%> TransVozMedia ;

! Consume
xup%<v%>%<tv%>:xup%> TransVozActiva ;
xup%<v%>%<iv%>%<aps%>:xúup%> TransVozAntipasiva ;
xup%<v%>%<iv%>%<pss%>:xuʼup%> TransVozPasiva ;
xup%<v%>%<iv%>%<mv%>:xùup%> TransVozMedia ;


LEXICON IrregularIntransVerb
xíimbal%<v%>%<iv%>:xíimbal TransVozAntipasiva ;
xíimbal%<v%>%<tv%>:xíimba%{l%}t TransVozActiva ;


LEXICON IrregularTransVerb

! to know (conocer)
k'ahóol%<v%>%<tv%>:k'ahóol%> TransitiveSetB ;

! to know (saber)
oheel%<v%>%<tv%>:oheel%> TransitiveSetB ;

! see
il%<v%>%<tv%>:il%> TransVozActiva ;
il%<v%>%<iv%>%<aps%>:ilah%> TransVozAntipasiva ;
il%<v%>%<iv%>%<pss%>:il%> IrregularTransVozPasiva ;

! hear/feel
uʼuy%<v%>%<tv%>:uʼuy%> TransVozActiva ;
uʼuy%<v%>%<iv%>%<aps%>:uʼuyah%> TransVozAntipasiva ;
uʼuy%<v%>%<iv%>%<pss%>:uʼuy%> IrregularTransVozPasiva ;

! The ʼbʼ is only used in the 3rd person....should I add the tag for subj_num=3?
uʼub%<v%>%<tv%>:uʼub%> TransVozActiva ;
uʼub%<v%>%<iv%>%<aps%>:uʼubah%> TransVozAntipasiva ;
uʼub%<v%>%<iv%>%<pss%>:uʼub%> IrregularTransVozPasiva ;

! say
aʼal%<v%>%<tv%>:aʼal%> TransVozActiva ;
aʼal%<v%>%<iv%>%<aps%>:aʼalah%> TransVozAntipasiva ;
aʼal%<v%>%<iv%>%<pss%>:aʼal%> IrregularTransVozPasiva ;

! Ask
kʼáat%<v%>%<tv%>:kʼáat%> TransVozActiva ;
kʼáat%<v%>%<iv%>%<aps%>:kʼáat%> TransVozAntipasiva ;
kʼáat%<v%>%<iv%>%<pss%>:kʼeáat%> TransVozPasiva ;


LEXICON PositionalInflections
%<ipfv%>:tal%> PluralSubjSuffix ;
%<pfv%>:lah%> IntransitiveSetB ;
%<pf%>:lahaʼan%> IntransitiveSetB ;
%<subj%>:lak%> IntransitiveSetB ;

LEXICON Positionals
! pucker
bochʼ%<v%>%<pos%>:bochʼ PositionalInflections ;

! stoop, squat, crouch, cower (lower eyes)
motʼ%<v%>%<pos%>:motʼ PositionalInflections ;

! compress, reduce
tsʼan%<v%>%<pos%>:tsʼan PositionalInflections ;

! gather, crease
muchʼ%<v%>%<pos%>:muchʼ PositionalInflections ;

! punch, puncture, bore
tsʼop%<v%>%<pos%>:tsʼop PositionalInflections ;

! pile up, gather (pile up earth, bury)
mul%<v%>%<pos%>:mul PositionalInflections ;

! thrust into a hole
tsʼot%<v%>%<pos%>:tsʼot PositionalInflections ;

! lean against (limit, stop)
nak%<v%>%<pos%>:nak PositionalInflections ;

! sit cross-legged (squash)
tsʼuk%<v%>%<pos%>:tsʼuk PositionalInflections ;

! join, unit
nuch%<v%>%<pos%>:nuch PositionalInflections ;

! bow, grovel (bend)
chin%<v%>%<pos%>:chin PositionalInflections ;

! stare
pʼich%<v%>%<pos%>:pʼich PositionalInflections ;

! pin, fix or set in place
chʼik%<v%>%<pos%>:chʼik PositionalInflections ;

! jump, protrude (jump, leap)
pʼit%<v%>%<pos%>:pʼit PositionalInflections ;

! scatter, stretch out (spread extend)
hay%<v%>%<pos%>:hay PositionalInflections ;

! bend
pʼok%<v%>%<pos%>:pʼok PositionalInflections ;

! straddle, mount (mount)
hek%<v%>%<pos%>:hek PositionalInflections ;

! swell up (swell, inflate, develop)
pʼol%<v%>%<pos%>:pʼol PositionalInflections ;

! sink, insert (insert, encircle)
hup%<v%>%<pos%>:hup PositionalInflections ;

! lean, bend
pʼus%<v%>%<pos%>:pʼus PositionalInflections ;

! place between two (same) things
kap%<v%>%<pos%>:kap PositionalInflections ;

! open, open wide (open, cut open)
xel%<v%>%<pos%>:xel PositionalInflections ;

! roll up, curl up, coil up (roll, twist, wind)
kotsʼ%<v%>%<pos%>:kotsʼ PositionalInflections ;

! open  (open, spread/extend, try for first time; e.g., xeen a xit a wiit/go open your anus for 1st time!)
xit%<v%>%<pos%>:xit PositionalInflections ;

! roll up, twist oneself, fold oneself up (roll, twist, screw)
kop%<v%>%<pos%>:kop PositionalInflections ;

! open, come out, appear (unfold, spread, open as in hand, legs, etc.)
xitʼ%<v%>%<pos%>:xitʼ PositionalInflections ;

! lie athwart, block
kʼat%<v%>%<pos%>:kʼat PositionalInflections ;

! stick to, adhere (fasten, stick to, adhere)
takʼ%<v%>%<pos%>:takʼ PositionalInflections ;

! lean (bend, tilt)
kʼeb%<v%>%<pos%>:kʼeb PositionalInflections ;

! protrude (raise over oneʼs head)
tichʼ%<v%>%<pos%>:tichʼ PositionalInflections ;

! put between two things
lap%<v%>%<pos%>:lap PositionalInflections ;

! bulge, knot (twist, sprain, dislocate)
tukʼ%<v%>%<pos%>:tukʼ PositionalInflections ;

! join, unit
lot%<v%>%<pos%>:lot PositionalInflections ;

! distend, stick out, lie with stomach sticking out (protrude, inflate)
tʼes%<v%>%<pos%>:tʼes PositionalInflections ;

! bend, arch (bend, twist)
met%<v%>%<pos%>:met PositionalInflections ;

! expose, extend, tighten (extend, tighten)
tʼin%<v%>%<pos%>:tʼin PositionalInflections ;

! fold hands
moch%<v%>%<pos%>:moch PositionalInflections ;

! ball up, bulge (form ball, make round)
wol%<v%>%<pos%>:wol PositionalInflections ;

! submerge (sink, immerse, dip)
tʼub%<v%>%<pos%>:tʼub PositionalInflections ;

! hang down something gelatinous (drop hand)
tʼoy%<v%>%<pos%>:tʼoy PositionalInflections ;


LEXICON AdjectivalEndings
PluralSubjSuffix ;
Terminatives ;

LEXICON DeadjectivalVerbalEndings
%<iv%>%<ipfv%>:tal%> PluralSubjSuffix ;
%<iv%>%<ipfv%>:chahal%> PluralSubjSuffix ;
%<iv%>%<pfv%>:chah%> IntransitiveSetB ;
%<iv%>%<pf%>:chahaʼan%> IntransitiveSetB ;
%<iv%>%<pf%>:chahak%> IntransitiveSetB ;

LEXICON Adjectives
! good
maʼaloʼob%<adj%>:maʼaloʼob%> AdjectivalEndings ;
maʼaloʼob%<v%>:maʼaloʼob%> DeadjectivalVerbalEndings ;

! sick
kohaʼan%<adj%>:kohaʼan%> AdjectivalEndings ;
kohaʼan%<v%>:kohaʼan%> DeadjectivalVerbalEndings ;

! good
uts%<adj%>:uts%> AdjectivalEndings ;
uts%<v%>:uts%> DeadjectivalVerbalEndings ;

! more
màas%<adj%>:màas%> AdjectivalEndings ;
màas%<v%>:màas%> DeadjectivalVerbalEndings ;

! slow, delayed
xàan%<adj%>:xàan%> AdjectivalEndings ;
xàan%<v%>:xàan%> DeadjectivalVerbalEndings ;

! existing
yàan%<adj%>:yàan%> AdjectivalEndings ;
yàan%<v%>:yàan%> DeadjectivalVerbalEndings ;

! big
nohoch%<adj%>:nohoch%> AdjectivalEndings ;
nohoch%<v%>:nohoch%> DeadjectivalVerbalEndings ;

! expensive
koʼoh%<adj%>:koʼoh%> AdjectivalEndings ;
koʼoh%<v%>:koʼoh%> DeadjectivalVerbalEndings ;

! possible
páah%<adj%>:páah%> AdjectivalEndings ;
páah%<v%>:páah%> DeadjectivalVerbalEndings ;

! town (not an adj...?)
káah%<adj%>:káah%> AdjectivalEndings ;
káah%<v%>:káah%> DeadjectivalVerbalEndings ;



