!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
Multichar_Symbols

%{V%}

%<v%>    ! Verbo

%<n%>    ! Sustantivo
%<adj%>  ! Adjetivo
%<pl%>   ! Plural
%<s_pl%>   ! Sujeto Plural

%<prx%>  ! Proximal 'this _'
%<med%>  ! Medial 'that _'
%<dst%>  ! Distal 'that _ over there'

%<incomp%> ! "ongoing incomplete"
%<past%> ! "Completed simple past"
%<perf%> ! "Completed Present Perfect"
%<subjun%> ! "Subjunctive"
%<pron%>  ! "pronoun"
%<1sg_poss%>  ! "pronoun"
%<2sg_poss%>  ! "pronoun"
%<3sg_poss%>  ! "pronoun"
%<1pl_poss%>  ! "pronoun"
%<2pl_poss%>  ! "pronoun"
%<3pl_poss%>  ! "pronoun"

%<poss_nonownership%>
%<poss_comm%>
%<poss_internal%>

%<poss%>  ! "posesivo"


%<hab%>

%<neg%>

%<ipfv%> ! imperfectivo "Ongoing incomplete"
%<pfv%>  ! perfectivo "Simple Past"
%<pf%>   ! perfect "Completed Present"
%<subj%>  ! subjunctive
%<s_sg%> ! sujeto singular
%<imp%>  ! imperativo
%<s_pl%> ! sujeto plural

%<s_sg1%>
%<s_sg2%>
%<s_sg3%>
%<s_pl1%>
%<s_pl2%>
%<s_pl3%>

%<o_sg1%>
%<o_sg2%>
%<o_sg3%>
%<o_pl1%>
%<o_pl2%>
%<o_pl3%>

%<pron_1sg%>
%<pron_2sg%>
%<pron_3sg%>
%<pron_1pl%>
%<pron_2pl%>
%<pron_3pl%>

%<female_agentive%>
%<male_agentive%>

%<tv%>            !
%<iv%>            ! 

%<attr%>          ! Attributive
%<der_l%>         ! Adjective derivation
%<term%>          ! Terminative morpheme
%<top%>          ! Topicalized
%<loc%>           ! locative

%<pos%> ! positional


%<hab%>
%<dur%>
%<cont%>
%<cess%>
%<incep%>
%<des%>


%{o%}             ! Either 'o' or nothing
%{e%}             ! Either 'e' or nothing
%{V%}             ! Copy stem vowel
%{wy%}          ! start to vowel-initial verb stems either y- or w- depending on person of subject.

ʼ                ! Glottal, U+02BC
%>                ! Morpheme boundary

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root
CvcTrans ;
IntransReg ;
Positionals ;
SetA ;
PronombresIndependientes ;
Negative ;
IrregularTransVerb ;
AspectMarker ;
Adjectives ;

LEXICON AspectMarker
ImperfectiveAspectMarker ;
PerfectiveAspectMarker ;

LEXICON ImperfectiveAspectMarker
%<hab%>:k # ;
%<dur%>:taan # ;
%<cont%>:laili' # ;
%<cess%>:ts'o'ok # ;
%<cess%>:taant # ; ! how to distinguish this proximate terminative?

%<incep%>:ho'op # ;
%<incep%>:kaah # ;
%<incep%>:chuun # ;
%<incep%>:lek # ;

%<des%>:taak # ;

LEXICON PerfectiveAspectMarker
%<pfv%>%<tv%>:t # ;

%<s_sg1%>%<pron%>%<pfv%>%<tv%>:tin # ;
%<s_sg2%>%<pron%>%<pfv%>%<tv%>:ta # ;

! TODO: is this accurate? verify t + u
%<s_sg3%>%<pron%>%<pfv%>%<tv%>:tu # ; 

%<pfv%>%<iv%>:h # ;


LEXICON Negative
%<neg%>:ma'a # ;
%<neg%>:ma # ;


LEXICON SetA
%<s_sg1%>%<pron%>:in # ;
%<s_sg1%>%<pron%>%<hab%>:kin # ;
%<s_sg1%>%<pron%>%<dur%>:tin # ;
%<s_sg1%>%<pron%>%<pfv%>:tin # ;

%<s_sg2%>%<pron%>:a # ;
%<s_sg2%>%<pron%>%<hab%>:ka # ;
%<s_sg2%>%<pron%>%<dur%>:tan # ;


%<s_sg3%>%<pron%>:u # ;
%<s_sg3%>%<pron%>%<hab%>:ku # ;

%<s_pl1%>%<pron%>:k # ;

%<s_pl2%>%<pron%>:a # ;
%<s_pl2%>%<pron%>%<hab%>:ka # ;
%<s_pl2%>%<pron%>%<dur%>:tan # ;
%<s_pl2%>%<pron%>%<pfv%>:ta # ;

%<s_pl3%>%<pron%>:u # ;


%<1sg_poss%>%<pron%>:in # ;
%<2sg_poss%>%<pron%>:a # ;
%<3sg_poss%>%<pron%>:u # ;
%<1pl_poss%>%<pron%>:k # ;
%<2pl_poss%>%<pron%>:a # ;
%<3pl_poss%>%<pron%>:u # ;

LEXICON PronombresIndependientes
%<pron_1sg%>:téen Topicalizer ;
%<pron_2sg%>:téech Topicalizer ;
%<pron_3sg%>:leti Topicalizer ;

%<pron_1pl%>:to'on Topicalizer ;
%<pron_2pl%>:te'ex Topicalizer ;
%<pron_3pl%>:leti'o'ob Topicalizer ;

LEXICON Topicalizer
%<top%>:e' # ;
Terminatives ;

LEXICON TransitiveSetB
%<o_sg1%>:en%> PluralSubjSuffix ;
%<o_pl1%>:o'on%> PluralSubjSuffix ;
%<o_sg2%>:ech%> PluralSubjSuffix ;
%<o_pl2%>:e'ex%> PluralSubjSuffix ;
%<o_sg1%>:en%> PluralSubjSuffix ;
%<o_sg3%>:0%> PluralSubjSuffix ;
%<o_sg3%>:ih%> PluralSubjSuffix ;
%<o_pl3%>:o'ob%> PluralSubjSuffix ;
Reflexives ;

LEXICON Reflexives
%<refl_1sg%>:inbah%> Terminatives ;
%<refl_2sg%>:abah%> Terminatives ;
%<refl_3sg%>:ubah%> Terminatives ;
%<refl_1pl%>:kbah%> Terminatives ;
%<refl_2pl%>:abahe'ex%> Terminatives ;
%<refl_3pl%>:ubaho'ob%> Terminatives ;

LEXICON IntransitiveSetB
%<s_sg1%>:en Terminatives ;
%<s_pl1%>:o'on Terminatives ;
%<s_sg2%>:ech Terminatives ;
%<s_pl2%>:e'ex Terminatives ;
%<s_sg1%>:en Terminatives ;
%<s_sg3%>:0 Terminatives ;
%<s_pl3%>:o'ob Terminatives ;

LEXICON PluralSubjSuffix
%<s_pl%>:o'ob%> Terminatives ;
%<s_pl%>:e'ex%> Terminatives ;
Terminatives ;

LEXICON Terminatives
%<neg%>:i' # ;
%<loc%>:i' # ;

! these are endings to the aspect markers laili and taant, respectively
%<cont%>:e' # ; 
%<cess%>:e' # ;

%<top%>:e' # ;
# ;

LEXICON RegIntransVozActivaSinL
%<ipfv%>:0%> IntransitiveSetB;
%<pfv%>:0%> IntransitiveSetB;
%<pf%>:aha'an%> IntransitiveSetB;
%<subj%>:%{V%}k%> IntransitiveSetB;
%<imp%>%<s_2sg%>:nen%> Terminatives;
%<imp%>%<s_2pl%>:nene'ex%> Terminatives;

LEXICON RegIntransVozActivaConL
%<ipfv%>:%{V%}l%> IntransitiveSetB;
%<pfv%>:0%> IntransitiveSetB;
%<pf%>:aha'an%> IntransitiveSetB;
%<subj%>:%{V%}k%> IntransitiveSetB;
%<imp%>%<s_2sg%>:nen%> Terminatives;
%<imp%>%<s_2pl%>:nene'ex%> Terminatives;

LEXICON RegIntransDvT
%<ipfv%>:ik%> TransitiveSetB;
%<pfv%>:ah%> TransitiveSetB;
%<pf%>:mah%> TransitiveSetB;
%<subj%>:0%> TransitiveSetB;
%<imp%>%<s_2sg%>:eh%> Terminatives;
%<imp%>%<s_2sg%>:0%> Terminatives;
%<imp%>%<s_2pl%>:0%> PluralSubjSuffix;

LEXICON RegIntransDvTVozPasiva
%<ipfv%>:a'al%> IntransitiveSetB;
%<pfv%>:a'ab%> IntransitiveSetB;
%<pf%>:a'an%> IntransitiveSetB;
%<subj%>:a'ak%> IntransitiveSetB;

! TODO: check on this....what's the plural ending?
%<imp%>:a'ak%> Terminatives;

LEXICON RegIntransDvTVozAntipasiva

%<ipfv%>:ah%> IntransitiveSetB;
%<pfv%>:ahnah%> IntransitiveSetB;
%<pf%>:ahnaha'an%> IntransitiveSetB;
%<subj%>:ahnak%> IntransitiveSetB;

! TODO: check on this....what's the plural ending?
%<imp%>:ahnak%> Terminatives;

LEXICON TransVozActiva
%<ipfv%>:ik%> TransitiveSetB;
%<pfv%>:ah%> TransitiveSetB;
%<pf%>:mah%> TransitiveSetB;
%<subj%>:0%> TransitiveSetB;
%<subj%>:eh%> TransitiveSetB;
%<imp%>:0%> TransitiveSetB;
%<imp%>:eh%> TransitiveSetB;


LEXICON TransVozAntipasiva
%<ipfv%>:0%> PluralSubjSuffix;

! TODO: for all of the following entries, the geminate "n%>n -> n"
%<pfv%>:nah%> IntransitiveSetB;
%<pf%>:naha'an%> IntransitiveSetB;
%<subj%>%<s_sg%>:nak%> IntransitiveSetB;
%<imp%>:nen%> IntransitiveSetB;

LEXICON TransVozPasiva
%<ipfv%>:%{V%}l%> PluralSubjSuffix;
%<pfv%>:a'ab%> IntransitiveSetB;
%<pf%>:a'an%> IntransitiveSetB;
%<subj%>:%{V%}k%> IntransitiveSetB;
! %<imp%>:%{V%}k%> IntransitiveSetB;  ! this is kind of weird, eg "be loved!" TODO verify that it is a valid form

LEXICON IrregularTransVozPasiva
%<ipfv%>:a'al%> PluralSubjSuffix;
%<pfv%>:a'ab%> IntransitiveSetB;
%<pf%>:a'an%> IntransitiveSetB;
%<subj%>:a'ak%> IntransitiveSetB;
! %<imp%>:a'ak%> IntransitiveSetB;

LEXICON TransVozMedia
%<ipfv%>:%{V%}l%> PluralSubjSuffix;
%<pfv%>:0%> IntransitiveSetB;
%<pf%>:a'an%> IntransitiveSetB;
%<subj%>:%{V%}k%> IntransitiveSetB;
%<imp%>:0%> IntransitiveSetB;


LEXICON IntransReg
! go
bin%<v%>%<iv%>:bin%> RegIntransVozActivaSinL ;
bin%<v%>%<tv%>:bis%> RegIntransDvT ;
bin%<v%>%<iv%>%<pss%>:bins%> RegIntransDvTVozPasiva ;
bin%<v%>%<iv%>%<aps%>:bins%> RegIntransDvTVozAntipasiva ;

! arrive
tal%<v%>%<iv%>:tal%> RegIntransVozActivaConL ;
tal%<v%>%<tv%>:tas%> RegIntransDvT ;
tal%<v%>%<iv%>%<pss%>:tas%> RegIntransDvTVozPasiva ;
tal%<v%>%<iv%>%<aps%>:tas%> RegIntransDvTVozAntipasiva ;

! go about (pasear)
máan%<v%>%<iv%>:máan%> RegIntransVozActivaConL ;
máan%<v%>%<tv%>:máans%> RegIntransDvT ;
máan%<v%>%<iv%>%<pss%>:máans%> RegIntransDvTVozPasiva ;
máan%<v%>%<iv%>%<aps%>:máans%> RegIntransDvTVozAntipasiva ;

! sleep
wen%<v%>%<iv%>:wen%> RegIntransVozActivaConL ;
wen%<v%>%<tv%>:wens%> RegIntransDvT ;
wen%<v%>%<iv%>%<pss%>:wens%> RegIntransDvTVozPasiva ;
wen%<v%>%<iv%>%<aps%>:wens%> RegIntransDvTVozAntipasiva ;

! wake up
ah%<v%>%<iv%>:%{wy%}ah%> RegIntransVozActivaConL ;
ah%<v%>%<tv%>:%{wy%}ahs%> RegIntransDvT ;
ah%<v%>%<iv%>%<pss%>:%{wy%}ahs%> RegIntransDvTVozPasiva ;
ah%<v%>%<iv%>%<aps%>:%{wy%}ahs%> RegIntransDvTVozAntipasiva ;

! finish, to be done
ts'o'ok%<v%>%<iv%>:ts'o'ok%> RegIntransVozActivaConL ;
ts'o'ok%<v%>%<tv%>:ts'o'oks%> RegIntransDvT ;
ts'o'ok%<v%>%<iv%>%<pss%>:ts'o'oks%> RegIntransDvTVozPasiva ;
ts'o'ok%<v%>%<iv%>%<aps%>:ts'o'oks%> RegIntransDvTVozAntipasiva ;

! reply
núuk%<v%>%<iv%>:núuk%> RegIntransVozActivaConL ;
núuk%<v%>%<tv%>:núuk%> RegIntransDvT ;
núuk%<v%>%<iv%>%<pss%>:núuks%> RegIntransDvTVozPasiva ;
núuk%<v%>%<iv%>%<aps%>:núuks%> RegIntransDvTVozAntipasiva ;

! enter
ok%<v%>%<iv%>:ok%> RegIntransVozActivaConL ;
ok%<v%>%<tv%>:oks%> RegIntransDvT ;
ok%<v%>%<iv%>%<pss%>:oks%> RegIntransDvTVozPasiva ;
ok%<v%>%<iv%>%<aps%>:oks%> RegIntransDvTVozAntipasiva ;

! exit
hóok%<v%>%<iv%>:hóok%> RegIntransVozActivaConL ;
hóok%<v%>%<tv%>:hóoks%> RegIntransDvT ;
hóok%<v%>%<iv%>%<pss%>:hóoks%> RegIntransDvTVozPasiva ;
hóok%<v%>%<iv%>%<aps%>:hóoks%> RegIntransDvTVozAntipasiva ;

! swim
báab%<v%>%<iv%>:báab%> RegIntransVozActivaConL ;
báab%<v%>%<tv%>:báabs%> RegIntransDvT ;
báab%<v%>%<iv%>%<pss%>:báabs%> RegIntransDvTVozPasiva ;
báab%<v%>%<iv%>%<aps%>:báabs%> RegIntransDvTVozAntipasiva ;

! swim
ichkíil%<v%>%<iv%>:ichkíil%> RegIntransVozActivaSinL ;
ichkíil%<v%>%<tv%>:ichkíilt%> RegIntransDvT ;
ichkíil%<v%>%<iv%>%<pss%>:ichkíilt%> RegIntransDvTVozPasiva ;
ichkíil%<v%>%<iv%>%<aps%>:ichkíilt%> RegIntransDvTVozAntipasiva ;

! raise up/ascend
líik%<v%>%<iv%>:líik%> RegIntransVozActivaConL ;
líik%<v%>%<tv%>:líiks%> RegIntransDvT ;
líik%<v%>%<iv%>%<pss%>:líiks%> RegIntransDvTVozPasiva ;
líik%<v%>%<iv%>%<aps%>:líiks%> RegIntransDvTVozAntipasiva ;

! lower/descend
éem%<v%>%<iv%>:éem%> RegIntransVozActivaConL ;
éem%<v%>%<tv%>:éems%> RegIntransDvT ;
éem%<v%>%<iv%>%<pss%>:éems%> RegIntransDvTVozPasiva ;
éem%<v%>%<iv%>%<aps%>:éems%> RegIntransDvTVozAntipasiva ;

! fall
lúub%<v%>%<iv%>:lúub%> RegIntransVozActivaConL ;
lúub%<v%>%<tv%>:lúubs%> RegIntransDvT ;
lúub%<v%>%<iv%>%<pss%>:lúubs%> RegIntransDvTVozPasiva ;
lúub%<v%>%<iv%>%<aps%>:lúubs%> RegIntransDvTVozAntipasiva ;


! comer
han%<v%>%<iv%>:han%> RegIntransVozActivaConL ;
han%<v%>%<tv%>:hant%> RegIntransDvT ;
han%<v%>%<iv%>%<pss%>:hant%> RegIntransDvTVozPasiva ;


LEXICON CvcTrans

! Open Question_ Should they lemmatize to the cvc transitive root in all voices?

! paint
bon%<v%>%<tv%>:bon%> TransVozActiva ;
bon%<v%>%<iv%>%<aps%>:bòon%> TransVozAntipasiva ;
bon%<v%>%<iv%>%<pss%>:bo'on%> TransVozPasiva ;
bon%<v%>%<iv%>%<mv%>:bóon%> TransVozMedia ;

! split apart
buh%<v%>%<tv%>:buh%> TransVozActiva ;
buh%<v%>%<iv%>%<aps%>:bùuh%> TransVozAntipasiva ;
buh%<v%>%<iv%>%<pss%>:bu'uh%> TransVozPasiva ;
buh%<v%>%<iv%>%<mv%>:búuh%> TransVozMedia ;

! cover, clothe
buk%<v%>%<tv%>:buk%> TransVozActiva ;
buk%<v%>%<iv%>%<aps%>:bùuk%> TransVozAntipasiva ;
buk%<v%>%<iv%>%<pss%>:bu'uk%> TransVozPasiva ;
buk%<v%>%<iv%>%<mv%>:búuk%> TransVozMedia ;

! sink, submerge
bul%<v%>%<tv%>:bul%> TransVozActiva ;
bul%<v%>%<iv%>%<aps%>:bùul%> TransVozAntipasiva ;
bul%<v%>%<iv%>%<pss%>:bu'ul%> TransVozPasiva ;
bul%<v%>%<iv%>%<mv%>:búul%> TransVozMedia ;

! throw, aim
ch'in%<v%>%<tv%>:ch'in%> TransVozActiva ;
ch'in%<v%>%<iv%>%<aps%>:ch'ìin%> TransVozAntipasiva ;
ch'in%<v%>%<iv%>%<pss%>:ch'i'in%> TransVozPasiva ;
ch'in%<v%>%<iv%>%<mv%>:ch'íin%> TransVozMedia ;

! moisten
ch'ul%<v%>%<tv%>:ch'ul%> TransVozActiva ;
ch'ul%<v%>%<iv%>%<aps%>:ch'ùul%> TransVozAntipasiva ;
ch'ul%<v%>%<iv%>%<pss%>:ch'u'ul%> TransVozPasiva ;
ch'ul%<v%>%<iv%>%<mv%>:ch'úul%> TransVozMedia ;

! Hang
ch'uy%<v%>%<tv%>:ch'uy%> TransVozActiva ;
ch'uy%<v%>%<iv%>%<aps%>:ch'ùuy%> TransVozAntipasiva ;
ch'uy%<v%>%<iv%>%<pss%>:ch'u'uy%> TransVozPasiva ;
ch'uy%<v%>%<iv%>%<mv%>:ch'úuy%> TransVozMedia ;

! Catch, fish
chuk%<v%>%<tv%>:chuk%> TransVozActiva ;
chuk%<v%>%<iv%>%<aps%>:chùuk%> TransVozAntipasiva ;
chuk%<v%>%<iv%>%<pss%>:chu'uk%> TransVozPasiva ;
chuk%<v%>%<iv%>%<mv%>:chúuk%> TransVozMedia ;

! Extend
hay%<v%>%<tv%>:hay%> TransVozActiva ;
hay%<v%>%<iv%>%<aps%>:háay%> TransVozAntipasiva ;
hay%<v%>%<iv%>%<pss%>:ha'ay%> TransVozPasiva ;
hay%<v%>%<iv%>%<mv%>:hàay%> TransVozMedia ;

! Grind
huch'%<v%>%<tv%>:huch'%> TransVozActiva ;
huch'%<v%>%<iv%>%<aps%>:húuch'%> TransVozAntipasiva ;
huch'%<v%>%<iv%>%<pss%>:hu'uch'%> TransVozPasiva ;
huch'%<v%>%<iv%>%<mv%>:hùuch'%> TransVozMedia ;

! sink
hup%<v%>%<tv%>:hup%> TransVozActiva ;
hup%<v%>%<iv%>%<aps%>:húup%> TransVozAntipasiva ;
hup%<v%>%<iv%>%<pss%>:hu'up%> TransVozPasiva ;
hup%<v%>%<iv%>%<mv%>:hùup%> TransVozMedia ;

! Change
k'ex%<v%>%<tv%>:k'ex%> TransVozActiva ;
k'ex%<v%>%<iv%>%<aps%>:k'éex%> TransVozAntipasiva ;
k'ex%<v%>%<iv%>%<pss%>:k'e'ex%> TransVozPasiva ;
k'ex%<v%>%<iv%>%<mv%>:k'èex%> TransVozMedia ;

! Learn
kan%<v%>%<tv%>:kan%> TransVozActiva ;
kan%<v%>%<iv%>%<aps%>:káan%> TransVozAntipasiva ;
kan%<v%>%<iv%>%<pss%>:ka'an%> TransVozPasiva ;
kan%<v%>%<iv%>%<mv%>:kàan%> TransVozMedia ;

! Pull
kol%<v%>%<tv%>:kol%> TransVozActiva ;
kol%<v%>%<iv%>%<aps%>:kóol%> TransVozAntipasiva ;
kol%<v%>%<iv%>%<pss%>:ko'ol%> TransVozPasiva ;
kol%<v%>%<iv%>%<mv%>:kòol%> TransVozMedia ;

! Sell
kon%<v%>%<tv%>:kon%> TransVozActiva ;
kon%<v%>%<iv%>%<aps%>:kóon%> TransVozAntipasiva ;
kon%<v%>%<iv%>%<pss%>:ko'on%> TransVozPasiva ;
kon%<v%>%<iv%>%<mv%>:kòon%> TransVozMedia ;

! Swallow
luk'%<v%>%<tv%>:luk'%> TransVozActiva ;
luk'%<v%>%<iv%>%<aps%>:lúuk'%> TransVozAntipasiva ;
luk'%<v%>%<iv%>%<pss%>:lu'uk'%> TransVozPasiva ;
luk'%<v%>%<iv%>%<mv%>:lùuk'%> TransVozMedia ;

! Grab
mach'%<v%>%<tv%>:mach'%> TransVozActiva ;
mach'%<v%>%<iv%>%<aps%>:máach'%> TransVozAntipasiva ;
mach'%<v%>%<iv%>%<pss%>:ma'ach'%> TransVozPasiva ;
mach'%<v%>%<iv%>%<mv%>:màach'%> TransVozMedia ;

! Eat Fast, “stuff-face”
mak'%<v%>%<tv%>:mak'%> TransVozActiva ;
mak'%<v%>%<iv%>%<aps%>:máak'%> TransVozAntipasiva ;
mak'%<v%>%<iv%>%<pss%>:ma'ak'%> TransVozPasiva ;
mak'%<v%>%<iv%>%<mv%>:màak'%> TransVozMedia ;

! Buy
man%<v%>%<tv%>:man%> TransVozActiva ;
man%<v%>%<iv%>%<aps%>:máan%> TransVozAntipasiva ;
man%<v%>%<iv%>%<pss%>:ma'an%> TransVozPasiva ;
man%<v%>%<iv%>%<mv%>:màan%> TransVozMedia ;

! Bury
muk%<v%>%<tv%>:muk%> TransVozActiva ;
muk%<v%>%<iv%>%<aps%>:múuk%> TransVozAntipasiva ;
muk%<v%>%<iv%>%<pss%>:mu'uk%> TransVozPasiva ;
muk%<v%>%<iv%>%<mv%>:mùuk%> TransVozMedia ;

! Plant
p'ak%<v%>%<tv%>:p'ak%> TransVozActiva ;
p'ak%<v%>%<iv%>%<aps%>:p'áak%> TransVozAntipasiva ;
p'ak%<v%>%<iv%>%<pss%>:p'a'ak%> TransVozPasiva ;
p'ak%<v%>%<iv%>%<mv%>:p'àak%> TransVozMedia ;

! Open Eyes
p'il%<v%>%<tv%>:p'il%> TransVozActiva ;
p'il%<v%>%<iv%>%<aps%>:p'íil%> TransVozAntipasiva ;
p'il%<v%>%<iv%>%<pss%>:p'i'il%> TransVozPasiva ;
p'il%<v%>%<iv%>%<mv%>:p'ìil%> TransVozMedia ;

! Measure
p'is%<v%>%<tv%>:p'is%> TransVozActiva ;
p'is%<v%>%<iv%>%<aps%>:p'íis%> TransVozAntipasiva ;
p'is%<v%>%<iv%>%<pss%>:p'i'is%> TransVozPasiva ;
p'is%<v%>%<iv%>%<mv%>:p'ìis%> TransVozMedia ;

! Wash
p'o%<v%>%<tv%>:p'o%> TransVozActiva ;
p'o%<v%>%<iv%>%<aps%>:p'óo%> TransVozAntipasiva ;
p'o%<v%>%<iv%>%<pss%>:p'o'o%> TransVozPasiva ;
p'o%<v%>%<iv%>%<mv%>:p'òo%> TransVozMedia ;

! Insult
poch'%<v%>%<tv%>:poch'%> TransVozActiva ;
poch'%<v%>%<iv%>%<aps%>:póoch'%> TransVozAntipasiva ;
poch'%<v%>%<iv%>%<pss%>:po'och'%> TransVozPasiva ;
poch'%<v%>%<iv%>%<mv%>:pòoch'%> TransVozMedia ;

! Talk, Speak
t'an%<v%>%<tv%>:t'an%> TransVozActiva ;
t'an%<v%>%<iv%>%<aps%>:t'áan%> TransVozAntipasiva ;
t'an%<v%>%<iv%>%<pss%>:t'a'an%> TransVozPasiva ;
t'an%<v%>%<iv%>%<mv%>:t'àan%> TransVozMedia ;

! Overflow
t'ul%<v%>%<tv%>:t'ul%> TransVozActiva ;
t'ul%<v%>%<iv%>%<aps%>:t'úul%> TransVozAntipasiva ;
t'ul%<v%>%<iv%>%<pss%>:t'u'ul%> TransVozPasiva ;
t'ul%<v%>%<iv%>%<mv%>:t'ùul%> TransVozMedia ;

! Burn (in fire)
tok%<v%>%<tv%>:tok%> TransVozActiva ;
tok%<v%>%<iv%>%<aps%>:tóok%> TransVozAntipasiva ;
tok%<v%>%<iv%>%<pss%>:to'ok%> TransVozPasiva ;
tok%<v%>%<iv%>%<mv%>:tòok%> TransVozMedia ;

! Fuck 
top%<v%>%<tv%>:top%> TransVozActiva ;
top%<v%>%<iv%>%<aps%>:tóop%> TransVozAntipasiva ;
top%<v%>%<iv%>%<pss%>:to'op%> TransVozPasiva ;
top%<v%>%<iv%>%<mv%>:tòop%> TransVozMedia ;

! Cure
ts'ak%<v%>%<tv%>:ts'ak%> TransVozActiva ;
ts'ak%<v%>%<iv%>%<aps%>:ts'áak%> TransVozAntipasiva ;
ts'ak%<v%>%<iv%>%<pss%>:ts'a'ak%> TransVozPasiva ;
ts'ak%<v%>%<iv%>%<mv%>:ts'àak%> TransVozMedia ;

! Peel, Skin
ts'il%<v%>%<tv%>:ts'il%> TransVozActiva ;
ts'il%<v%>%<iv%>%<aps%>:ts'íil%> TransVozAntipasiva ;
ts'il%<v%>%<iv%>%<pss%>:ts'i'il%> TransVozPasiva ;
ts'il%<v%>%<iv%>%<mv%>:ts'ìil%> TransVozMedia ;

! Fry
tsa%<v%>%<tv%>:tsa%> TransVozActiva ;
tsa%<v%>%<iv%>%<aps%>:tsáa%> TransVozAntipasiva ;
tsa%<v%>%<iv%>%<pss%>:tsa'a%> TransVozPasiva ;
tsa%<v%>%<iv%>%<mv%>:tsàa%> TransVozMedia ;

! Urinate
wix%<v%>%<tv%>:wix%> TransVozActiva ;
wix%<v%>%<iv%>%<aps%>:wíix%> TransVozAntipasiva ;
wix%<v%>%<iv%>%<pss%>:wi'ix%> TransVozPasiva ;
wix%<v%>%<iv%>%<mv%>:wìix%> TransVozMedia ;

! Vomit
xeh%<v%>%<tv%>:xeh%> TransVozActiva ;
xeh%<v%>%<iv%>%<aps%>:xéeh%> TransVozAntipasiva ;
xeh%<v%>%<iv%>%<pss%>:xe'eh%> TransVozPasiva ;
xeh%<v%>%<iv%>%<mv%>:xèeh%> TransVozMedia ;

! Study, read
xok%<v%>%<tv%>:xok%> TransVozActiva ;
xok%<v%>%<iv%>%<aps%>:xóok%> TransVozAntipasiva ;
xok%<v%>%<iv%>%<pss%>:xo'ok%> TransVozPasiva ;
xok%<v%>%<iv%>%<mv%>:xòok%> TransVozMedia ;

! Cut
xot%<v%>%<tv%>:xot%> TransVozActiva ;
xot%<v%>%<iv%>%<aps%>:xóot%> TransVozAntipasiva ;
xot%<v%>%<iv%>%<pss%>:xo'ot%> TransVozPasiva ;
xot%<v%>%<iv%>%<mv%>:xòot%> TransVozMedia ;

! Slice
xot'%<v%>%<tv%>:xot'%> TransVozActiva ;
xot'%<v%>%<iv%>%<aps%>:xóot'%> TransVozAntipasiva ;
xot'%<v%>%<iv%>%<pss%>:xo'ot'%> TransVozPasiva ;
xot'%<v%>%<iv%>%<mv%>:xòot'%> TransVozMedia ;

! Consume
xup%<v%>%<tv%>:xup%> TransVozActiva ;
xup%<v%>%<iv%>%<aps%>:xúup%> TransVozAntipasiva ;
xup%<v%>%<iv%>%<pss%>:xu'up%> TransVozPasiva ;
xup%<v%>%<iv%>%<mv%>:xùup%> TransVozMedia ;


LEXICON IrregularTransVerb

! see
il%<v%>%<tv%>:il%> TransVozActiva ;
il%<v%>%<iv%>%<aps%>:ilah%> TransVozAntipasiva ;
il%<v%>%<iv%>%<pss%>:il%> IrregularTransVozPasiva ;

! hear/feel
u'uy%<v%>%<tv%>:u'uy%> TransVozActiva ;
u'uy%<v%>%<iv%>%<aps%>:u'uyah%> TransVozAntipasiva ;
u'uy%<v%>%<iv%>%<pss%>:u'uy%> IrregularTransVozPasiva ;

! The 'b' is only used in the 3rd person....should I add the tag for subj_num=3?
u'ub%<v%>%<tv%>:u'ub%> TransVozActiva ;
u'ub%<v%>%<iv%>%<aps%>:u'ubah%> TransVozAntipasiva ;
u'ub%<v%>%<iv%>%<pss%>:u'ub%> IrregularTransVozPasiva ;

! say
a'al%<v%>%<tv%>:a'al%> TransVozActiva ;
a'al%<v%>%<iv%>%<aps%>:a'alah%> TransVozAntipasiva ;
a'al%<v%>%<iv%>%<pss%>:a'al%> IrregularTransVozPasiva ;


LEXICON PositionalInflections
%<ipfv%>:tal%> PluralSubjSuffix ;
%<pfv%>:lah%> IntransitiveSetB ;
%<pf%>:laha'an%> IntransitiveSetB ;
%<subj%>:lak%> IntransitiveSetB ;

LEXICON Positionals
! pucker
boch'%<v%>%<pos%>:boch' PositionalInflections ;

! stoop, squat, crouch, cower (lower eyes)
mot'%<v%>%<pos%>:mot' PositionalInflections ;

! compress, reduce
ts'an%<v%>%<pos%>:ts'an PositionalInflections ;

! gather, crease
much'%<v%>%<pos%>:much' PositionalInflections ;

! punch, puncture, bore
ts'op%<v%>%<pos%>:ts'op PositionalInflections ;

! pile up, gather (pile up earth, bury)
mul%<v%>%<pos%>:mul PositionalInflections ;

! thrust into a hole
ts'ot%<v%>%<pos%>:ts'ot PositionalInflections ;

! lean against (limit, stop)
nak%<v%>%<pos%>:nak PositionalInflections ;

! sit cross-legged (squash)
ts'uk%<v%>%<pos%>:ts'uk PositionalInflections ;

! join, unit
nuch%<v%>%<pos%>:nuch PositionalInflections ;

! bow, grovel (bend)
chin%<v%>%<pos%>:chin PositionalInflections ;

! stare
p'ich%<v%>%<pos%>:p'ich PositionalInflections ;

! pin, fix or set in place
ch'ik%<v%>%<pos%>:ch'ik PositionalInflections ;

! jump, protrude (jump, leap)
p'it%<v%>%<pos%>:p'it PositionalInflections ;

! scatter, stretch out (spread extend)
hay%<v%>%<pos%>:hay PositionalInflections ;

! bend
p'ok%<v%>%<pos%>:p'ok PositionalInflections ;

! straddle, mount (mount)
hek%<v%>%<pos%>:hek PositionalInflections ;

! swell up (swell, inflate, develop)
p'ol%<v%>%<pos%>:p'ol PositionalInflections ;

! sink, insert (insert, encircle)
hup%<v%>%<pos%>:hup PositionalInflections ;

! lean, bend
p'us%<v%>%<pos%>:p'us PositionalInflections ;

! place between two (same) things
kap%<v%>%<pos%>:kap PositionalInflections ;

! open, open wide (open, cut open)
xel%<v%>%<pos%>:xel PositionalInflections ;

! roll up, curl up, coil up (roll, twist, wind)
kots'%<v%>%<pos%>:kots' PositionalInflections ;

! open  (open, spread/extend, try for first time; e.g., xeen a xit a wiit/go open your anus for 1st time!)
xit%<v%>%<pos%>:xit PositionalInflections ;

! roll up, twist oneself, fold oneself up (roll, twist, screw)
kop%<v%>%<pos%>:kop PositionalInflections ;

! open, come out, appear (unfold, spread, open as in hand, legs, etc.)
xit'%<v%>%<pos%>:xit' PositionalInflections ;

! lie athwart, block
k'at%<v%>%<pos%>:k'at PositionalInflections ;

! stick to, adhere (fasten, stick to, adhere)
tak'%<v%>%<pos%>:tak' PositionalInflections ;

! lean (bend, tilt)
k'eb%<v%>%<pos%>:k'eb PositionalInflections ;

! protrude (raise over one's head)
tich'%<v%>%<pos%>:tich' PositionalInflections ;

! put between two things
lap%<v%>%<pos%>:lap PositionalInflections ;

! bulge, knot (twist, sprain, dislocate)
tuk'%<v%>%<pos%>:tuk' PositionalInflections ;

! join, unit
lot%<v%>%<pos%>:lot PositionalInflections ;

! distend, stick out, lie with stomach sticking out (protrude, inflate)
t'es%<v%>%<pos%>:t'es PositionalInflections ;

! bend, arch (bend, twist)
met%<v%>%<pos%>:met PositionalInflections ;

! expose, extend, tighten (extend, tighten)
t'in%<v%>%<pos%>:t'in PositionalInflections ;

! fold hands
moch%<v%>%<pos%>:moch PositionalInflections ;

! ball up, bulge (form ball, make round)
wol%<v%>%<pos%>:wol PositionalInflections ;

! submerge (sink, immerse, dip)
t'ub%<v%>%<pos%>:t'ub PositionalInflections ;

! hang down something gelatinous (drop hand)
t'oy%<v%>%<pos%>:t'oy PositionalInflections ;


LEXICON AdjectivalEndings
PluralSubjSuffix ;
Terminatives ;

LEXICON DeadjectivalVerbalEndings
%<iv%>%<ipfv%>:tal%> PluralSubjSuffix ;
%<iv%>%<ipfv%>:chahal%> PluralSubjSuffix ;
%<iv%>%<pfv%>:chah%> IntransitiveSetB ;
%<iv%>%<pf%>:chaha'an%> IntransitiveSetB ;
%<iv%>%<pf%>:chahak%> IntransitiveSetB ;

LEXICON Adjectives
! good
ma'alo'ob%<adj%>:ma'alo'ob%> AdjectivalEndings ;
ma'alo'ob%<v%>:ma'alo'ob%> DeadjectivalVerbalEndings ;

! sick
koha'an%<adj%>:koha'an%> AdjectivalEndings ;
koha'an%<v%>:koha'an%> DeadjectivalVerbalEndings ;

! good
uts%<adj%>:uts%> AdjectivalEndings ;
uts%<v%>:uts%> DeadjectivalVerbalEndings ;

! more
màas%<adj%>:màas%> AdjectivalEndings ;
màas%<v%>:màas%> DeadjectivalVerbalEndings ;

! slow, delayed
xàan%<adj%>:xàan%> AdjectivalEndings ;
xàan%<v%>:xàan%> DeadjectivalVerbalEndings ;

! existing
yàan%<adj%>:yàan%> AdjectivalEndings ;
yàan%<v%>:yàan%> DeadjectivalVerbalEndings ;

! big
nohoch%<adj%>:nohoch%> AdjectivalEndings ;
nohoch%<v%>:nohoch%> DeadjectivalVerbalEndings ;

! expensive
ko'oh%<adj%>:ko'oh%> AdjectivalEndings ;
ko'oh%<v%>:ko'oh%> DeadjectivalVerbalEndings ;

! possible
páah%<adj%>:páah%> AdjectivalEndings ;
páah%<v%>:páah%> DeadjectivalVerbalEndings ;

! town (not an adj...?)
káah%<adj%>:káah%> AdjectivalEndings ;
káah%<v%>:káah%> DeadjectivalVerbalEndings ;


