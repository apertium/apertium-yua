Multichar_Symbols

%<n%>    ! Noun
%<pl%>   ! Plural

%<prx%>  ! Proximal 'this _'
%<med%>  ! Medial 'that _'
%<dst%>  ! Distal 'that _ over there'

%<attr%> ! Attributive
%<der_l%> ! Adjective derivation

%{o%}    ! Either 'o' or nothing
%{V%}    ! Copy stem vowel

%>       ! Morpheme boundary

LEXICON Root

Nouns ;

LEXICON Dem

%<prx%>:%>a # ;
%<med%>:%>o # ;
%<dst%>:%>e # ;
            # ; 

LEXICON Deriv

%<der_l%>:%>%{V%}l # ; 
%<attr%>:%>il # ;

LEXICON N-PEK ! -(o)ob

%<n%>%<pl%>:%>%{o%}ob Dem ; ! Plural
%<n%>:                Dem ; ! Unmarked
%<n%>:                Deriv ; ! Derivations

LEXICON N-NAL ! -i 

%<n%>%<pl%>:%>i       Dem ; ! Plural
%<n%>:                Dem ; ! Unmarked
%<n%>:                Deriv ; ! Derivations

LEXICON N-AC  ! -oob, -i

%<n%>%<pl%>:%>i       Dem ; ! Plural
%<n%>%<pl%>:%>%{o%}ob Dem ; ! Plural
%<n%>:                Dem ; ! Unmarked
%<n%>:                Deriv ; ! Derivations

LEXICON N-PAL ! -al, -aloob

%<n%>%<pl%>:%>al      Dem ; ! Plural
%<n%>%<pl%>:%>aloob   Dem ; ! Plural
%<n%>:                Dem ; ! Unmarked
%<n%>:                Deriv ; ! Derivations

LEXICON N-IXIM ! singularia tantum

%<n%>:                Dem ; ! Unmarked
%<n%>:                Deriv ; ! Derivations


LEXICON Nouns

pek:pek N-PEK ; ! "perro, dog"
na:na N-PEK ; ! "casa, house"
tzimn:tzimn N-PEK ; ! "caballo, horse"
cħicħ:cħicħ N-PEK ; ! "pájaro"
be:be N-PEK ; ! "camino"
ku:ku N-PEK ; ! "nido" 
nal:nal N-NAL ; ! "maís"
ixim:ixim N-IXIM ; ! ""
pal:pal N-PAL ; ! "child"
cay:cay N-PEK ; ! "" 
ac:ac N-AC ; ! ""
uah:uah N-PEK ; ! "tortilla"

caan:caan N-PEK ; ! "" ! TODO: Check pl
cab:cab N-PEK ; ! "" ! TODO: Check pl
pet:pet N-PEK ; ! "" ! TODO: Check pl
xib:xib N-PEK ; ! "" ! TODO: Check pl
boc:boc N-PEK ; ! "" ! TODO: Check pl
cħup:cħup N-PEK ; ! "" ! TODO: Check pl

